% TXL Turing Plus basis grammar
% J.R. Cordy, GMD Karlsruhe, 14.03.91
% (from yacc grammar for T+, R.C. Holt)

% and later hacked by K.A. Schneider to be used for 
% embedding prolog facts into Turing Plus source.
% circa. April 1992

% and tuned by JRC June 92

compounds
	:= .. >> >>> <= >= => ** 
	+= -= *= /= **= 
end compounds

comments
    	'%
    	/* */
end comments

keys
	const init pervasive var collection of forward
	bind to type set int real boolean string
	enum array record 'end union label pointer
	'import body procedure proc 'function fcn
	pre post module 'export invariant begin
	'assert new free tag if then else elsif
	loop exit when case for decreasing return
	result put skip get true false div mod
	'not and or in all opaque 
	int1 int2 int4 nat1 nat2 nat4 nat real4 real8
	stub grant parent monitor child condition priority
	deferred timeout 'external register char addressint
	packed process handler unqualified read write seek tell
	open quit fork asm signal wait pause bits xor shl shr close 
	checked unchecked 'include
end keys


% required by TXL
define program
    	[compilation]
end define


define compilation
	[mainProgram]
end define


define grantList
	grant (  [list optVarId] ) [NL]
end define


define parentClause
	parent [stringlit] [NL]
end define


define externalDeclaration
	'external [opt stringlit] 	% external name override
	[subprogramHeader] [EX]
end define


define mainProgram
	[repeat declarationOrStatement]
end define


define stubDeclaration
	[constantDeclaration]	[KEEP]
    |   [variableDeclaration]	[KEEP]
    |	[collectionDeclaration]	[KEEP]
    |   [typeDeclaration]	[KEEP]
    |	[externalDeclaration]	[KEEP]
    |	[stubFunctionHeader][EX]	[KEEP]
    |	[stubProcedureHeader][EX]	[KEEP]
    |  	[grantList]
    |   [exportList]
    |   [importList]
    |   [preClause]
    |   [postClause]
    |   [initClause]
end define


define declaration
	[constantDeclaration]	[KEEP]
    |   [variableDeclaration]	[KEEP]
    |	[collectionDeclaration]	[KEEP]
    |   [typeDeclaration]	[KEEP]
    |	[subprogramDeclaration]	[KEEP]		% JRC
    |	[externalDeclaration]	[KEEP]
    |	[externalFunctionHeader]	[KEEP]
    |	[forwardFunctionHeader]	[KEEP]
    |	[externalProcedureHeader]	[KEEP]
    |	[forwardProcedureHeader]	[KEEP]
    |	[moduleDeclaration]	[KEEP]
    |   [stubModuleDeclaration] [KEEP]
    |   [bodyModuleDeclaration] [KEEP]
    |	[monitorDeclaration]	[KEEP]
    |	[conditionDeclaration]	[KEEP]

    |	[parentClause]
    |	body [stringlit] [NL]
    |  	[grantList]
    |	child [stringlit] [NL]
    |   [exportList]
    |   [importList]
    |   [preClause]
    |   [postClause]
    |   [initClause]
    |   [exceptionHandler]
end define

define subprogramDeclaration		% JRC
    	[procedureDeclaration]
    |	[bodyProcedureDeclaration]
    |	[functionDeclaration]
    |	[bodyFunctionDeclaration]
end define

define constantDeclaration
    	const [opt 'register] [opt pervasiveSpec] [id] [opt colon_typeSpec] 
	    := [initializingValue]
end define


define colon_typeSpec
	: [typeSpec]
end define


define initializingValue
	[expn]
    |	[IN] [NL] init ( [list initializingValue] ) [EX]
end define


define pervasiveSpec
	pervasive
    |	*
end define


define variableDeclaration
    	[opt externalSpec] var [opt 'register] [list id+] 
	    [colonTypeSpec_or_colonEqualInitializingValue]
end define


define colonTypeSpec_or_colonEqualInitializingValue
	: [typeSpec] [opt colonEqualInitializingValue]
    |	[colonEqualInitializingValue]
end define


define collectionDeclaration
    	var [list id+] : [opt 'unchecked] collection of [typeSpec_or_forwardId]
end define


define typeSpec_or_forwardId
	[typeSpec]
    |	forward [id]
end define


define externalSpec
	'external [opt expn]  % external address
end define


define conditionDeclaration
    	var [list id+] : [opt arrayOf] condition [opt conditionOption]
end define


define conditionOption
	priority
    |	deferred
    |	timeout
end define


define arrayOf
	array [list indexType+] of
    |	{ [list indexType+] }
end define


define colonEqualInitializingValue
	:= [initializingValue]
end define


define variableBinding
	bind [list bindClause+]
	| bind [bindClause]
end define


define bindClause
	[opt 'var] [opt 'register] [reference] to [reference]
%	[opt 'var] [opt 'register] [id] to [variableReference]
end define


define optVarId
	[opt 'var] [grantId]
end define
define grantId
	[id]
end define


define typeDeclaration
	type [opt pervasiveSpec] [id] : [typeSpec]
end define


% Type Specifications 

define typeSpec
	[opt 'packed] [unpackedTypeSpec]
end define


define unpackedTypeSpec
	[standardType]
    |	[subrangeType] [opt storageSize]
    |	[enumeratedType]
    |	[arrayType]
    |	set of [baseType]
    |	[recordType]
    |	[unionType]
    |	[pointerType]
    |	[subprogramType]
    |	[namedType]
end define


define standardType
	int
    |	int1
    |	int2
    |	int4
    |	nat
    |	nat1
    |	nat2
    |	nat4
    |	real
    |	real4
    |	real8
    |	boolean
    |	string	[opt parenthesizedCompileTimeExpn]
    |	char	[opt parenthesizedExpn]
    |	addressint
end define


define subprogramType
	'procedure [id] [opt parameterListDeclaration]
    |	'function [id] [opt parameterListDeclaration] [opt id] : [typeSpec]
end define


define storageSize
	: [compileTimeExpn]
end define


define parenthesizedCompileTimeExpn
	( [compileTimeExpn] )
end define

define parenthesizedExpn
	( [expn] )
end define


define subrangeType
	[compileTimeExpn] .. [expn]
end define


define enumeratedType
	enum ( [list id+] )
end define


define arrayType
	array [list indexType+] of [typeSpec]
    |	{ [list indexType+] } [typeSpec]
end define


define baseType
	[indexType]
end define


define indexType
	[subrangeType]
    |	[enumeratedType]
    |   [namedType]
end define


define recordType
	record [NL]
	    [repeat recordFieldDeclaration+]
	'end record
end define


define recordFieldDeclaration
	[list id+]  : [typeSpec] [opt ';] [NL]
end define


define unionType
	union [opt id] : [indexType] of [IN] [IN] [NL]
	    [repeat unionAlternativeDeclaration+]
	    [opt unionOtherwiseDeclaration] [EX] [EX]
	'end union
end define


define unionAlternativeDeclaration
	[EX] [label_or_bar] [list compileTimeExpn+] : [IN] [NL]
	    [repeat unionFieldDeclaration+]
end define


define unionOtherwiseDeclaration
	[EX] [label_or_bar] : [IN] [NL]
	    [repeat unionFieldDeclaration+]
end define


define label_or_bar
	label 
    |	'|
end define


define unionFieldDeclaration
	[list id+] : [typeSpec] [opt ';] [NL]
end define


define pointerType
	pointer to [collectionId]
end define


define namedType
	[reference]
end define


define collectionId
	[id]
end define



% Subprogram Declarations 


define bodyFunctionDeclaration
	[opt 'MARK] [opt id] 'body 'function [id] [opt ';] [IN][NL]
	[repeat declarationOrStatement] [EX]
    'end [id]
end define

define functionDeclaration
	[opt 'MARK] [opt id] 'function [id] [opt parameterListDeclaration] [opt id] : [typeSpec] [opt ';] [IN][NL]
	[repeat declarationOrStatement] [EX]
    'end [id]
end define

define bodyProcedureDeclaration
    [opt 'MARK] [opt id] 'body 'procedure [id] [opt ';] [IN][NL]
	[repeat declarationOrStatement] [EX]
    'end [id]
end define

define procedureDeclaration
    [opt 'MARK] [opt id] 'procedure [id] [opt parameterListDeclaration] [opt ';] [IN][NL] %[opt importList]
	[repeat declarationOrStatement] [EX]
    'end [id]
end define

define subprogramHeader
    	'procedure [id] [opt parameterListDeclaration] [opt ';][NL]
	| 'function [id] [opt parameterListDeclaration] [opt id] : [typeSpec] [opt ';] [NL]
end define

define stubProcedureHeader
    	'procedure [id] [opt parameterListDeclaration] [opt ';][NL]
end define


define stubFunctionHeader
	'function [id] [opt parameterListDeclaration] [opt id] : [typeSpec] [opt ';] [NL]
end define

define forwardProcedureHeader
    	'forward 'procedure [id] [opt parameterListDeclaration] [opt ';] [IN][NL]%[opt importList][EX]
end define


define forwardFunctionHeader
	'forward 'function [id] [opt parameterListDeclaration] [opt id] : [typeSpec] [opt ';] [IN][NL]%[opt importList][EX]
end define

define externalProcedureHeader
    	'external 'procedure [id] [opt parameterListDeclaration] [opt ';] 
end define

define externalFunctionHeader
    	'external 'function [id] [opt parameterListDeclaration] [opt ';] 
end define


define parameterListDeclaration
	( [parameterDeclarationList] )
end define


define parameterDeclarationList
	[list parameterDeclaration+] [opt commaVaryingParameterDeclaration]
    |	[id] : ..	% (only) varying parameter
end define


define commaVaryingParameterDeclaration
	, [id] : ..	% (last) varying parameter
end define


define parameterDeclaration
	[subprogramHeader]
    |	[opt 'MARK] [opt 'register] [opt 'var] [list id+] : [parameterType]
    |   [prologFact]
end define


define parameterType
     	[typeSpec]
    |	type [typeSpec]
    |	[stringCharStar]
    |	array [list compileTimeExpnDotDotStar+] of [typeSpec]
    |	{ [list compileTimeExpnDotDotStar+] } [typeSpec]
    |	array [list compileTimeExpnDotDotStar+] of [stringCharStar]
    |	{ [list compileTimeExpnDotDotStar+] } [stringCharStar]
end define


define compileTimeExpnDotDotStar
    	[compileTimeExpn] .. *
end define


define stringCharStar
	string ( * )
    | 	char   ( * )
end define


define importList
	'import ( [list importItem] ) [NL]
    |	'import [list importItem] [NL]
end define


define importItem
	[opt 'forward] [opt 'var] [id] 
end define


define subprogramBody
	[repeat declarationOrStatement] [EX]
    'end [id]
end define


define preClause
	pre [booleanExpn] [NL]
end define


define initClause
	init [list idColonEqualExpn+] [NL]
end define


define idColonEqualExpn
	[id] := [compileTimeExpn] 
end define
    

define postClause
	post [booleanExpn] [NL]
end define


define exceptionHandler
	handler ( [id] ) [NL] [IN]
	    [repeat declarationOrStatement] [EX]
	'end handler [NL]
end define


define stubModuleDeclaration
	stub module [id] [IN] [NL]
	    [repeat stubDeclaration]
	    [EX]
	'end [id]
end define

define bodyModuleDeclaration
	[opt 'MARK] body module [id] [IN] [NL]
	    [repeat declarationOrStatement]
	    [EX]
	'end [id]
end define

define moduleDeclaration
	[opt 'MARK] module [id] [IN] [NL]
	    [repeat declarationOrStatement]
	    [EX]
	'end [id]
end define


define monitorDeclaration
	monitor [id] [IN] [NL]
	    [opt colonExpn]
	    [repeat declarationOrStatement]
	    [EX]
	'end [id]
end define


define exportList
	'export ( [list exportItem+] ) [NL]
    |	'export [list exportItem+] [NL]
end define


define exportItem
	[opt 'opaque] [opt unqualifiedSpec] [id]
end define


define unqualifiedSpec
	unqualified
    |	~ .
    |	'not .
end define



% Statements 

define declarationOrStatement
	[declaration]		[opt ';] [NL] 
    |	[variableBinding]	[opt ';] [NL] 
    |	[statement]		[opt ';] [NL] 
    |	[prologFact]		[opt ';] [NL] 
end define

define prologFact
    	'$ 'rType ( [id], [resourceType] ) '$		[opt ';] [NL] 
    |	'$ 'var ( [id] )	 '$	[opt ';] [NL] 
    |	'$ 'const ( [id] )	 '$	[opt ';] [NL] 
    |	'$ 'type ( [id] )	 '$	[opt ';] [NL] 
    |	'$ 'procedure ( [id] ) '$		[opt ';] [NL] 
    |	'$ 'function ( [id] )	 '$	[opt ';] [NL] 
    |	'$ 'module ( [id] )	 '$	[opt ';] [NL] 
    |	'$  'process ( [id] )	 '$	[opt ';] [NL] 
    |	'$  'monitor ( [id] )	 '$	[opt ';] [NL] 
    |	'$  'ico ( [id], [id] ) '$		[opt ';] [NL] 
    |	'$  'importConst ( [id], [id] ) '$		[opt ';] [NL] 
    |	'$  'importVar ( [id], [id] )	 '$	[opt ';] [NL] 
    |	'$  'exportOpaque ( [id], [id] ) '$		[opt ';] [NL] 
    |	'$  'exportClear ( [id], [id] ) '$		[opt ';] [NL] 
    |	'$  'varPar ( [id] )	 '$	[opt ';] [NL] 
    |	'$  'constPar ( [id] )	 '$	[opt ';] [NL] 
    |	'$  'getRef ( [id], [id] ) '$		[opt ';] [NL] 
    |	'$  'putRef ( [id], [id] ) '$		[opt ';] [NL] 
    |	'$  'parRef ( [id], [id] ) '$		[opt ';] [NL] 
    |	'$  'procCall ( [id], [id] ) '$		[opt ';] [NL] 
end define
define resourceType
	'var | 'const | 'type | 'procedure | 'function 
       | 'module | 'process | 'monitor 
end define


define statement
	[assignmentStatement]	[KEEP]
    |	[callStatement]		[KEEP]
    |	'assert [booleanExpn]	[KEEP]
    |	[returnStatement]	[KEEP]
    |	[resultStatement]	[KEEP]
    |	[ifStatement]		[KEEP]
    |	[loopStatement]		[KEEP]
    |	[exitStatement]		[KEEP]
    |	[caseStatement]		[KEEP]
    |	begin [IN] [NL]
	    [repeat declarationOrStatement] [EX]
	'end			[KEEP]
    |	new [id] , [variableReference]	[KEEP]
    |	free [id] , [variableReference]	[KEEP]
    |	[forStatement]		[KEEP]
    |	tag [variableReference] , [expn] [KEEP]
    |	[putStatement]		[KEEP]
    |	[getStatement]		[KEEP]
    |	[readOrWriteStatement]	[KEEP]
    |	[seekStatement]		[KEEP]
    |	[tellStatement]		[KEEP]
    |	[openStatement]		[KEEP]
    |	close : [expn]		[KEEP]
    |	close ( [expn] )	[KEEP]
    |	[quitStatement]		[KEEP]
    |	[forkStatement]		[KEEP]
    |	[assemblerStatement]	[KEEP]
    |	[signalStatement]	[KEEP]
    |	[waitStatement]		[KEEP]
    |	[pauseStatement]	[KEEP]
    |	unchecked		[KEEP]
    |	checked			[KEEP]
    |	invariant [booleanExpn]  [opt ';] [KEEP]
    |	'include [stringlit]	[KEEP]
end define


define assignmentStatement
	[variableReference] [assignmentOperator] [expn]
end define


define ifStatement
	if [booleanExpn] then [IN] [NL]
	    [repeat declarationOrStatement] [EX]
	  [repeat elsifClause] 
	  [opt elseClause]
	'end if

    |	'[ [booleanExpn] : [IN] [NL]
	    [repeat declarationOrStatement] [EX]
	  [repeat shortElsifClause] 
	  [opt shortElseClause]
	']
end define


define elsifClause 
	elsif [booleanExpn] then  [IN] [NL]
	    [repeat declarationOrStatement] [EX]
end define


define elseClause
	else  [IN] [NL]
	    [repeat declarationOrStatement] [EX]
end define


define shortElsifClause
	'| [booleanExpn] : [IN] [NL]
	    [repeat declarationOrStatement] [EX]
end define


define shortElseClause
	'| : [IN] [NL]
	    [repeat declarationOrStatement] [EX]
end define


define loopStatement
	loop [IN] [NL]
	    [opt invariantClause]
	    [repeat declarationOrStatement] [EX]
	'end loop

    |	{  [IN] [NL]
	    [opt invariantClause]
	    [repeat declarationOrStatement] [EX]
	}
end define


define invariantClause
	invariant [booleanExpn] [NL]
end define


define returnStatement
	return
    |	>>>
end define


define resultStatement
	result [expn]
    |	>>> : [expn]
end define


define exitStatement
	exit [opt whenBooleanExpn]
    |	>> [opt colonBooleanExpn]
end define


define whenBooleanExpn
	when [booleanExpn]
end define


define colonBooleanExpn
	: [booleanExpn]
end define


define caseStatement
	case [expn] of [IN] [IN] [NL]
	    [repeat caseAlternative]
	    [opt caseOtherwise] [EX] [EX]
	'end case
end define


define caseAlternative
	[EX] [label_or_bar] [list compileTimeExpn+] : [IN] [NL]
	    [repeat declarationOrStatement] 
end define


define caseOtherwise
	[EX] [label_or_bar] : [IN] [NL]
	    [repeat declarationOrStatement]
end define


define forStatement
	for [opt 'decreasing] [opt id] : [forRange] [IN] [NL]
	    [opt invariantClause]
	    [repeat declarationOrStatement] [EX]
	'end for

    |	{ [plusOrMinus] [opt id] : [forRange] [IN] [NL]
	    [opt invariantClause]
	    [repeat declarationOrStatement] [EX]
	}
end define


define forRange
	[expn] .. [expn]
    |	[namedType]
end define


define plusOrMinus
        + | -
end define


define callStatement
	[reference]
end define


define putStatement
	put [opt streamSpec] [list putItem+] [opt '..]
    |	! [opt streamSpec] [list putItem+] [opt '..]
end define


define streamSpec
	: [expn] ,
end define


define putItem
	[expn] [opt colonExpn] [opt colonExpn] [opt colonExpn]
    |	skip
end define


define colonExpn
	: [expn]
end define


define getStatement
	get [opt streamSpec] [list getItem+]
    |	? [opt streamSpec] [list getItem+]
end define


define getItem
	[variableReference] [opt getWidth]
    |	skip
end define


define getWidth
	: [expn]
    |	: *
end define


define readOrWriteStatement
	read : [expn] [opt colonStatus] , [list readWriteItem+]
    |	write : [expn] [opt colonStatus] , [list readWriteItem+]
end define


define colonStatus
	: [variableReference]
end define


define readWriteItem
	[variableReference] [opt colonExpn] [opt colonVariableReference]
end define


define colonVariableReference
  	: [variableReference]
end define


define seekStatement
	seek : [expn] , [expn]
    |	seek : [expn] , *
end define


define tellStatement
	tell : [expn] , [variableReference]
end define


define openStatement
	open : [variableReference] , [expn] , [list ioCapability+]
    |	open ( [variableReference] , [expn] , [expn] )
end define


define ioCapability
	get
    |	put
    |	read
    |	write
    |	seek
end define


define quitStatement
	quit [opt guiltyParty] [opt colonExpn]
end define


define guiltyParty
	>
    |	<
end define


define forkStatement
	fork [id] [opt parameters] [opt forkParameters]
end define


define forkParameters
	: [variableReference] [opt commaExpn] [opt commaVariableReference]
end define


define commaExpn
	, [expn]
end define


define commaVariableReference
 	, [variableReference]
end define


define parameters
    ( [list expn+] )
end define


define assemblerStatement
	asm [stringlit] [opt colonStringLit] [repeat commaExpn]
end define


define colonStringLit
 	: [stringlit]
end define


define signalStatement
	signal [variableReference]
end define


define waitStatement
	wait [variableReference] [opt commaExpn]
end define


define pauseStatement
	pause [expn]
end define


% References 

define variableReference
    	[reference]
    |	# [variableReference]
    |	bits ( [variableReference] , [bitsRange] )
end define


define reference
	[id] [repeat componentSelector]
|	[prologFact] [repeat componentSelector]
    |	type ( [typeCheatSpec] , [variableReference] ) [repeat componentSelector]
    |	[typeCheatSpec] @ ( [expn] ) [repeat componentSelector]
end define


define typeCheatSpec
	[standardType]
    |	[id]
end define


define bitsRange
	[subrangeType]
    |	[compileTimeExpn]  % namedType is a subset of compileTimeExpn -- handle semantically
end define
    

define componentSelector
	( [list expn] ) 
    |	. [id] 
end define


% Expressions 

define booleanExpn
	[expn]
end define


define compileTimeExpn
	[expn]
end define


define expn
    	[unaryExpn] [repeat binaryOperatorUnaryExpn]
end define


define binaryOperatorUnaryExpn
	[binaryOperator] [unaryExpn]
end define


define binaryOperator
	=>  |  or  |  xor  |  and  |  & 
     |  <  |  >  |  =  |  <=  |  >=  |  [notSpec] =  |  in  |  [notSpec] in 
     |  +  |  -  |  *  |  /  |  div  |  mod  |  shl  |  shr  |  ** 
end define


define unaryExpn
	[opt unaryOperator] [subExpn]
end define


define unaryOperator
	[notSpec]  |  +  |  -  |  #
end define


define notSpec
	'not  |  ~
end define


define assignmentOperator
	:=  |  +=  |  -=  |  xor=  |  *=  |  /=  |  div=  |  mod= 
    |	shl=  |  shr=  |  **=  |  and=  |  or=
end define


define subExpn
	[reference] [opt substringOrSetConstructor]
    |	[explicitConstant]
    |	( [expn] )
end define


define explicitConstant
	[integernumber]
    |	[floatnumber]
    |	[stringlit]
    |	[charlit]	
    |	true
    |	false
end define


define substringOrSetConstructor
    	( [substringPosition] [opt dotDotSubstringPosition] )
    |	( [opt 'all] )
end define

define substringPosition
    	* [opt minusExpn]
    |	[expn]
end define

define dotDotSubstringPosition
	.. [substringPosition]
end define

define minusExpn
	- [expn]
end define
