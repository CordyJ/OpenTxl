% Powerbuilder Transformer
% Steve O'Hara
% Trinity Millennium Group, Inc.
% June 3, 2008
%

tokens 
	stringlit "\"[(~\c)#\"]*\"" 
end tokens

comments
	'$PBExportComments$
end comments

define program
	[repeat srd_statement]
end define

define srd_statement
	[ExportHeader] [NL]
    |	[Release] [NL]
    |	[SrdCommand] [NL]
    |	[Table] [NL]
    |	[comment] [NL]
end define

define ExportHeader
	'$ [SPOFF] 'PBExportHeader '$ [fileName] [SPON]
end define

define fileName
	[id] '. [id]
end define

define Release
	'release [number] ';
end define

define SrdCommand
	[SrdKeyword] '( [NL] [IN] [repeat Param] [EX] ')
    |	'data '( [NL] [IN] [repeat dataItem] [EX] ')
end define

define dataItem
	'null [SPOFF] [maybeComma] [SPON] [NL]
    |	[number] [SPOFF] [maybeComma] [SPON] [NL]
    |	[stringlit] [SPOFF] [maybeComma] [SPON] [NL]
    |	[SPOFF] [number] '- [number] '- [number] [SP]
    		[number] ': [number] ': [number] ': [number]
    		[maybeComma] [SPON] [NL]
end define

define maybeComma
	',
    |	[empty]
end define

define SrdKeyword
	'datawindow
    |	'header
    |	'header [SPOFF] '[ [number] '] [SPON]
    |	'summary
    |	'footer
    |	'detail
    |	'column
    |	'compute
    |	'line
    |	'text
    |	'groupbox
    |	'group
    |	'report
    |	'sparse
    |	'rectangle
    |	'bitmap
    |	'htmltable
    |	'tableblob
    |	'htmlgen
    |	'graph
    |	'crosstab
end define

define Param
	[Key] '= [Value] [NL]
    |	'name '= [id] [NL]
    |	'type '= 'char [SPOFF] '( [number] ') [SPON] [NL]
    |	'type '= 'decimal [SPOFF] '( [number] ') [SPON] [NL]
    |	'by '= '( [list stringlit] ') [SPON] [NL]
    |	'by '= '( [repeat stringlit] ') [SPON] [NL]
    |	'nest_arguments '= '( [list nestArg] ')
end define

define nestArg
	'( [stringlit] ')
end define

define Key
	[id]
    |	[id] [SPOFF] '. [id] [SPON]
    |	[id] [SPOFF] '. [id] '. [id] [SPON]
    |	[id] [SPOFF] '. [id] '. [id] '. [id] [SPON]
    |	[id] [SPOFF] '. '3 'd [SPON]
end define

define Value
	[number]
    |	'- [SPOFF] [number] [SPON]
    |	[id]
    |	'no
    |	'number
    |	'header [SPOFF] '. [number] [SPON]
    |	'header [SPOFF] '[ [number] '] [SPON]
    |	'trailer [SPOFF] '. [number] [SPON]
    |	[stringlit]
    |	[SrdCommand]
end define

define Table
	'table '( [NL] [IN] [repeat Column] [repeat TableParam] [EX] [NL] ')
end define

define Column
	'column '= '( [NL] [IN] [repeat Param] [EX] ') [NL]
end define

define TableParam
	[id] '= [Value] [NL]
    |	'arguments '= '( [list Arg] ')
end define

define Arg
	'( [stringlit] ', [id] ')
end define
