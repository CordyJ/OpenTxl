% CheckParametersLib
% Darren Cousineau
% © Legasys Corp. 1997
% Version: 1.0.0
% Created: Friday, January 10, 1997


% The folowing set of functions checks for the existence of certain
% flags, and whether or not these flags have values associated
% with them.  There is also a set of functions to access the values 
% associated with these flags.

function checkHasValue Flag [stringlit] Msg [stringlit]
	match [stringlit*]
		AllParameters [stringlit*]
	where not all
		AllParameters [reportIfMissingFlag Flag Msg]
			      [reportIfMissingValue Flag Msg]
			      [reportIfValueIsFlag Flag Msg] 
end function


function CheckHasFlag Flag [stringlit] Msg [stringlit]
	match * [stringlit*]
		AllParameters [stringlit*]
	where not all
		AllParameters [reportIfMissingFlag Flag Msg]
end function

function reportIfMissingFlag Flag [stringlit] Msg [stringlit]
	match [stringlit*]
		AllParameters [stringlit*]
	deconstruct not * [stringlit*] AllParameters
		Flag Rest [stringlit*]
	construct ErrorMessage [stringlit]
		_ [+ "* "] [+ Msg] [+ " - Missing flag "] [+ Flag] [print] [quit 1] 
end function

function reportIfMissingValue Flag [stringlit] Msg [stringlit]
	match [stringlit*]
		AllParameters [stringlit*]
	deconstruct not * [stringlit*] AllParameters
		Flag Value [stringlit] Rest [stringlit*]
	construct ErrorMessage [stringlit]
		_ [+ "* "] [+ Msg] [+ " - Missing value for "] [+ Flag] [print] [quit 1] 
end function

function reportIfValueIsFlag Flag [stringlit] Msg [stringlit]
	match [stringlit*]
		AllParameters [stringlit*]
	deconstruct * [stringlit*] AllParameters
		Flag Value [stringlit] Rest [stringlit*]
	construct First [number]
		_ [index Value "-"]
	where
		First [= 1]
	construct ErrorMessage [stringlit]
		_ [+ "* "] [+ Msg] [+ " - Missing value for "] [+ Flag] [print] [quit 1] 
end function

function GetValueAsStringLit Flag [stringlit]
	replace [stringlit]
		Output [stringlit]
	import argv [stringlit*]
	deconstruct * [stringlit*] argv
		Flag Value [stringlit] Rest [stringlit*]
	by
		Value
end function

function GetValueAsId Flag [stringlit]
	replace [id]
		Output [id]
	import argv [stringlit*]
	deconstruct * [stringlit*] argv
		Flag Value [stringlit] Rest [stringlit*]
	construct NewId [id]
		_ [unquote Value]
	by
		NewId
end function