% LSCobol Standard Grammar
% D.A. Jardine & J.R. Cordy, Spring 1996
% D.A. Jardine December 1995

% Copyright 1995,1996 Legasys Corp.

% Derived from the BNS Cobol Grammar v4.2,
% originally developed Fall 1994 / Spring 1995 by D.A. Jardine & J.R. Cordy


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% This is the Legasys standard internal Cobol format grammar used by
% all LS2000 and other Legasys Cobol systems, except LSMetaCobol.
%
% LSCobol format has the following properties:
%
%	Col 1-6 and 73-80 stripped
%	Comments, REMARKS and NOTES statements removed
%	Compiler directives removed
%	All COPY,  COPY REPLACING, and REPLACE statements interpreted and removed
%	Continuation markers interpreted and removed
%	All missing and extra periods corrected
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% Revision History:
%
% 5.0	DAJ	1 Feb 1996	
%	Initial revision, from BNS Cobol Grammar 4.2 with RBC changes
%
% 5.0d1	DAJ/AJM	13 Feb 1996
%	Relation Condition grammar revised by AJM Jan 26 1996, for parsing Cobol 1 and Cobol 2
%
% 5.0d2	TD	12 March 1996	
%	Newlines to break up long lists (> 255 chars)
%	Changed 77_or_88 back to 77, and added 88 as subordinate to 77. 
%	Put compounds back in for <= >=	
%	Aimed in general direction of LS Cobol by removing BNS_dot, USW_Dot, etc. 
%	Removed [dot] from ON statement
%
% 5.1	TD	20 March 1996
%	New Version Numbers, changed number in subscript to numeric_literal.
%
% 5.2	TD	20 March 1996
%	Added rule to report grammar version number
%
% 5.3	TD	5 May 1996
%	Corrected SD section grammar
%
% 5.4	JRC	7 May 1996
%	Added EXEC CICS statement
%	Added missing [dot] to section headers in DECLARATIVES
%
% 5.5	JRC	10 May 96
%	Added ENTER LINKAGE
%
% 5.6	JRC	12 May 96
%	Integrated new section/paragraph grammar into main grammar
%	Corrected formatting of paragraphs
%
% 5.7	JRC	22 May 1996
%	Updated CICS grammar to IBM CICS manual specs
%
% 6.0	JRC/AJM	24 May 1996
%	General cleanup of entire grammar
%
% 6.1	JRC	12 June 1996
%	Updated CICS grammar to allow IBM ADDRESS OF extension on data names
%
% 6.2	JRC	26 June 1996
%	Removed backslash escape sequences from token definition for charlit
%
% 6.3   RATH	8,9 August 1996 - changes for CO system
%
%	Changed :   ACCEPT [id] [opt accept_clause]
%	     to :   ACCEPT [identifier] [opt accept_clause]
%	
%	Changed (in definition of int_or_dataname_or_index_name):
%	   	    |   [id] [opt plusminus_integer]
%	     to:    |   [identifier] [opt plusminus_integer]
% 
%	Changed :   define class_condition
%			[identifier] [opt 'IS] [opt 'NOT] [class_type]
%		    end define
%
%	     to :   define class_condition
%			[identifier_or_bracketed_identifier] [opt 'IS] [opt 'NOT] [class_type]
%		    end define
%
%		    define identifier_or_bracketed_identifier
%			[identifier]
%     			|  '( [identifier] ')
%		    end define
% 6.3   AJM	12 August 1996 - changes to correct formatting in MOMX and elsewhere.
%
%	When parsing [plusminus_integer] the lexical sequence [id], +, [number] is ambiguous.
%	For example: ÔA (B +3)Õ is an [identifier] of a two-dimensional subscript of A
%	using the values B and 3; but ÔA (B + 3)Õ is an [identifier] of a one-dimensional subscript
%	of A using the value B+3.  Since the latter is more likely, use it.
%	Change
%			define plusminus_integer
%				[SPOFF] [SP] '+ [number] [SPON]
%			    |   [SPOFF] [SP] '- [number] [SPON]
%			end define
% 	to
%			define plusminus_integer
%				[add_or_subtract] [numeric_literal]
%			end define
%	This also affects output display, as it should; a space appears between the [add_or_subtract]
%       and the [numeric_literal].
%
%	When parsing arithmetic expressions, the (optional) sign on numeric literals should not
%	be viewed as a unary operator. In fact, it's redundant in front of a numeric literal,
%	because the literal itself can have the optional sign.
%	Change
%			define arithmetic_primary
%				[opt sign] [identifier_or_literal]
%			    |   [opt sign] '( [arithmetic_expression] ')
%			    |   [function_reference]
%			end define
%	to			
%			define arithmetic_primary
%				[literal]
%			    |	[opt sign] [identifier]
%			    |	[opt sign] '( [arithmetic_expression] ')
%			    |	[function_reference]
%			end define
%
%	Hex literals have a spurious space after the X, which should be suppressed:
%	Change
%
%			define hex_literal
%				'X [charlit]
%			end define
%	to
%			define hex_literal
%				'X [SPOFF] [charlit] [SPON]
%			end define
%
% Similarly, there should be no space between the sign on a numeric literal and
% the number; change
%			define numeric_literal
%				 [number]
%			     |   [SPOFF] [sign] [number] [SPON]
%			end define
%
%			define numeric_literal
%				 [number]
%			     |   [sign] [SPOFF] [number] [SPON]
%			end define			
%
%
% 6.4   RATH	22 August 1996 - changes to changes for CO system
%
%	Changed (in definition of int_or_dataname_or_index_name) - which caused problems with
%		conflicts generated for table subscripts:
%	            |   [identifier] [opt plusminus_integer]
%		to: |   [id] [repeat qualifier] [opt subscript] [opt plusminus_integer]
% 
% 6.5	JRC	18 Sep 96
%	Changed the syntax of IF statements to disallow empty THEN and ELSE clauses.
%	This was both incorrect and was causing problems for TXL 1.5d3.  
%	This change should not affect any rulesets.
% 6.6   DRF  23 Sep 96
%	Combined v6.4 and v6.5 into same file
% 6.7   TRD  9 Dec 1996
%	- Added +-Number to token defintions
%       - Change numeric literal to reflct new token definition (remove second option)
%	- Updated declaratives_clause to reflect new paragraph grammar in sections.
%	- Changed condition_primary to have a level_88 condition that requires a ?
%	- Deleted duplicate condition_primary
% 6.8   TRD Dec 20/96
%       - added spaces arround +/- in addition_factor
%         so that subsequent parses to do not tokenize + .005 as +.005
%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% 7.0   TRD Jan 14, 1997
%	- New Name System (At end of grammar)
%	  - identifier, decl_name, ref_name, etc.
%	- New Record Item grammar
%	  - Report Records still use old grammar (and no reparse)
%	  - Combined all 4 non-terminals into a single record-item
%	  - parses 66's correctly.
%	  - 77's gone.
%	  - Removed warning about Context Free Parsing, hasn't been true
%	    since sometime in version 5.
%
% 7.0.1 - T.D. Feb 6, 1997
%	- Merged Global-Overrides.Grammar into Main Grammar.
%		- sort_order		
%		- DEPENDING_ON_id -> changed id to ref_name to allow qualified names
%		- repeat clauses in inspect statement
%	- Removed Compiler Directives (not refered to by any part of the grammar)
%	- object-factor silliness fixed.
%       - T.D. March 14, 1997
%	  - exec grammar inserted into main grammar
%	- T.D. March 21, 1997
%	  - handle empty search WHENs in [search_when_clause]
%	- T.D. April 9, 1997
%	  - fix subscripts to allow expressions in absence of numeric literal
%		override in bracket.
% 7.0.2 - T.D. April 21, 1997
%	- Changed ids in key clauses (Record key, nominal key, alternate key)
% 	  to ref_name so that they will be uid for consideration by hotspots.
% 7.0.3 - T.D. April 29, 1997
%	- Added backquoted strings as id's (new token defition)
%	- Allow 999-999 as an id (paragraph and section names)
%	- write_statement must accept identifiers
% 7.0.4 - RATH May 29, 1997
%	- Extended support for COBOL I [use_clause]'s to include:
%		USE BEFORE STANDARD LABEL PROCEDURE and
%		USE BEFORE REPORTING [data_name]
%	  (see COBOL I manual, pp. 170-177).
% 	- Added support for numeric paragraphs 999-9999 and hex literals
%	  eg. X'89'
%	- Added extension to allow COUNT IS as well as COUNT IN (there will
%	  be a corresponding rule in DialectConversion to change COUNT IS to
%	  COUNT IN)
%	- Added extension to allow expressions of the form BLAH NOT ZEROS, where
%	  the grammar used to insist on BLAH NOT EQUAL ZEROS.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Version reporting function
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

function GrammarVersion
    replace [stringlit]
    	CurString [stringlit]
    by
    	CurString [+ "LSCobol.Grammar v7.0.4 (May 20, 1997)"]
end function

define GrammarVersion
    "v7.0.4"
end define


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Lexical specification of LSCobol
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%# TXL pragma	-lscobol  -idchars '-/'

% Token extensions to handle Cobol identifiers and numbers
tokens 
	id 	+ "\d[\d-]*\a\i*"	% allow Cobol hyphenated identifiers as well as default
	id 	+ "\d+-[\d\i]+"		% Anything starting with 999-... is an id.
	id	+ "((\d[\d-]*\a\i*)*`[#`]+`(\d[\d-]*\a\i*)*)+" % Any chars between backquotes can be part of an id
	number	+ "[+-]?.\d+"		% allow Cobol numbers (e.g., .23) as well as default 
	number	+ "[+-]\d+(.\d+)?"	% Allow numbers with leading signs.
	charlit "'[('')#']*'"     	% allow escaped quotes in Cobol - DAJ 29.12.95 - JRC 26.6.96
	charlit + "X'[('')#']*'"	% Special hex literals (eg X'24')

    	%	Explanation of charlit token
        %        ' ... '         - start and end with a quote
        %        [ ... ]*        - inside the quotes, have any number
        %                          of any of the following two things:
        %        ('')            - first thing: two quotes in a row (the Cobol internal quote convention)
        %        #'              - second thing: any character that is not a quote
end tokens

% Cobol compound symbols. These must not have an intervening space.
compounds 
	<=  >= 
end compounds	

% Keywords of LSCobol
% Any that can appear in identifier contexts should not be keywords.

include "LSCobol7.KW"


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Syntactic specification of LSCobol
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% =================================================
% 		LSCobol Programs
% =================================================

define program
	[LSCobol_program]
end define

define LSCobol_program
	[identification_division]
        [opt environment_division]			
        [opt data_division]
        [opt procedure_division]
        [repeat nested_source_program]
        [opt end_clause]
end define

define nested_source_program 
	[identification_division]
        [opt environment_division]			
        [opt data_division]
        [opt procedure_division]
        [repeat nested_source_program]
	[end_clause]
end define

define end_clause
						[NL] 
	'END 'PROGRAM [id] [dot]		[NL] [NL]
end define


% =================================================
%              Identification Division
% =================================================
    
define identification_division
						    	[NL] [NL] [NL]
	[IDENTIFICATION_or_ID] 'DIVISION [dot] 		
	    [program_id_paragraph]
end define

define IDENTIFICATION_or_ID 
        'IDENTIFICATION | 'ID
end define 

define program_id_paragraph
	[NL] 
	'PROGRAM-ID [dot] [SP] [SP] [program_name] [opt common_initial_clause] [dot]
end define

define program_name
	[id_or_literal]
end define

define common_initial_clause
        [opt 'IS] [repeat COMMON_or_INITIAL+] [opt 'PROGRAM]
end define 

define COMMON_or_INITIAL
	'COMMON | 'INITIAL
end define


% =================================================
%               Environment Division.
% =================================================

define environment_division
				    		[NL] [NL] [NL]
    	'ENVIRONMENT 'DIVISION [dot]
    	[opt configuration_section]
    	[opt input_output_section]
    	[opt io_control_paragraph]
end define


% =================================================
%   Environment Division -- Configuration Section
% =================================================

define configuration_section
				    		[NL] [NL]
    	[opt CONFIGURATION_SECTION_dot] 				
	    [repeat configuration_paragraph]	
end define

define CONFIGURATION_SECTION_dot
	'CONFIGURATION 'SECTION [dot]
end define
	
define configuration_paragraph
        [source_computer_paragraph]
    |   [object_computer_paragraph]
    |   [special_names_paragraph]
    
end define 

define source_computer_paragraph
	[NL] 'SOURCE-COMPUTER [dot] [SP] [SP] [opt source_computer_entry]
end define

define source_computer_entry
        [id] [opt WITH_DEBUGGING_MODE] [dot]
end define 

define WITH_DEBUGGING_MODE 
        [opt 'WITH] 'DEBUGGING 'MODE
end define 

define object_computer_paragraph 
        [NL] 'OBJECT-COMPUTER [dot] [SP] [SP] [opt object_computer_entry]
end define 

define object_computer_entry
        [id] [repeat object_computer_attribute] [dot]
end define

define object_computer_attribute
        [NL] 'MEMORY [opt 'SIZE] [number] [WORDS_or_CHARACTERS_or_MODULES]
    |   [NL] [opt 'PROGRAM] [opt 'COLLATING] 'SEQUENCE [opt 'IS] [id]
    |   [NL] 'SEGMENT-LIMIT [opt 'IS] [number]
end define 

define WORDS_or_CHARACTERS_or_MODULES 
        'WORDS | 'CHARACTERS | 'MODULES
end define 

define special_names_paragraph 
        						[NL] 
	'SPECIAL-NAMES [dot] 				[IN] 				
	    [opt special_names_clauses]			[EX] 
end define

define special_names_clauses
	[repeat special_names_clause+] [dot]
end define

define special_names_clause
    	  [NL] [implementor_clause]
        | [NL] [alphabet_clause]
        | [NL] [symbolic_characters_clause]
        | [NL] [class_clause]
        | [NL] [currency_clause]
        | [NL] [decimal_point_clause]        
                
end define 

define implementor_clause
	[id] [opt IS_id] [repeat ON_OFF_STATUS]
end define 

define IS_id
	'IS [id]
end define

define ON_OFF_STATUS
        'ON [opt STATUS_IS] [id]
    |	'OFF [opt STATUS_IS] [id]
end define

define STATUS_IS
        'STATUS 'IS
end define

define alphabet_clause
        'ALPHABET [id] [opt 'IS] [repeat alphabet_type+]
end define 

define alphabet_type 
        'STANDARD | 'STANDARD-1 | 'STANDARD-2 | 'NATIVE | 'EBCDIC 
    |	[id] 
    |	[repeat explicit_alphabet_type+]
end define 

define explicit_alphabet_type 
        [literal] [opt THROUGH_or_ALSO_literal]
end define 

define THROUGH_or_ALSO_literal
        [THROUGH_or_THRU] [literal]
    |	[repeat ALSO_literal+]
end define

define ALSO_literal
        'ALSO [literal]
end define 
                 
define THROUGH_or_THRU
        'THROUGH | 'THRU
end define 

define symbolic_characters_clause
        'SYMBOLIC [opt 'CHARACTERS] [repeat symbolic_characters_entry]
end define

define symbolic_characters_entry
        [NL] [repeat id+] [opt IS_or_ARE] [repeat number+] [opt IN_alphabet]
end define

define IS_or_ARE 
        'ARE | 'IS
end define 

define IN_alphabet 
        'IN [id]
end define 
                      
define class_clause
        'CLASS [id] [opt 'IS] [repeat literal_THROUGH_literal+]
end define

define currency_clause
        'CURRENCY [opt 'SIGN] [opt 'IS] [literal]
end define

define decimal_point_clause
        'DECIMAL-POINT [opt 'IS] 'COMMA
end define


% =================================================
%   Environment Division -- Input-Output Section
% =================================================

define input_output_section
						[NL] [NL]
	'INPUT-OUTPUT 'SECTION [dot]							 			
	    [opt file_control_paragraph] 	
	    [opt io_control_paragraph] 		
end define

define file_control_paragraph
    						[NL]
	'FILE-CONTROL [dot] 			[IN]				 
	    [repeat file_control_entry] 	[EX]
end define

define file_control_entry
    						[NL] 
        'SELECT [opt 'OPTIONAL] [id] [TAB_25] 
	    [repeat file_control_clause] [dot]
end define

define file_control_clause
	[assign_to_clause]
    |	[reserve_clause]
    |   [organization_clause]
    |   [padding_clause]
    |   [record_delimiter_clause]
    |   [access_clause]
    |   [record_key_clause]
    |   [alternate_key_clause]
    |   [nominal_key_clause]
    |   [password_clause]
    |   [status_clause]
end define

define assign_to_clause
	'ASSIGN [opt 'TO] [repeat id_or_literal+]
end define

define reserve_clause
        [NL] 'RESERVE [NO_or_integer] [opt 'ALTERNATE] [opt AREA_or_AREAS]
end define

define NO_or_integer
        'NO | [number]
end define

define AREA_or_AREAS
        'AREA | 'AREAS
end define

define organization_clause
        [NL] [opt 'ORGANIZATION] [opt 'IS] [file_organization]
end define

define file_organization
        [opt 'LINE] 'SEQUENTIAL 
    |	'RELATIVE 
    |	'INDEXED
end define

define padding_clause
        [NL] 'PADDING [opt 'CHARACTER] [opt 'IS] [repeat id_or_literal+]
end define

define record_delimiter_clause
        [NL] 'RECORD 'DELIMITER [opt 'IS] [STANDARD_1_or_id]
end define

define STANDARD_1_or_id
        'STANDARD-1 | [id]
end define

define access_clause
        [NL] 'ACCESS [opt 'MODE] [opt 'IS] [access_mode] [opt relative_key]
end define

define access_mode
        'SEQUENTIAL | 'RANDOM | 'DYNAMIC
end define

define relative_key
        [NL] 'RELATIVE [opt 'KEY] [opt 'IS] [id]
end define

define password_clause
        [NL] 'PASSWORD [opt 'IS] [id]
end define

define status_clause
         [NL] [opt 'FILE] 'STATUS [opt 'IS] [id] [opt id]
end define

define record_key_clause
        [NL] 'RECORD [opt 'KEY] [opt 'IS] [ref_name] [opt password_clause]
end define

define nominal_key_clause
        [NL] 'NOMINAL [opt 'KEY] [opt 'IS] [ref_name]
end define

define  alternate_key_clause
        [NL] 'ALTERNATE [opt 'RECORD] [opt 'KEY] [opt 'IS] [ref_name] [opt WITH_DUPLICATES]
end define

define WITH_DUPLICATES
        [opt 'WITH] 'DUPLICATES [opt password_clause]
end define

define io_control_paragraph
						[NL]
	'I-O-CONTROL [dot]			[IN]				 
	    [opt io_control_clauses]		[EX]
end define

define io_control_clauses
        [repeat io_control_clause] [dot]
end define

define io_control_clause
        [NL] [rerun_clause]
    |   [NL] [same_clause]
    |   [NL] [multiple_clause]
    |   [NL] [apply_clause]
end define

define rerun_clause
        'RERUN [opt 'ON] [file_name_or_string] [opt 'EVERY] [rerun_criterion]
end define

define file_name_or_string
        [id]
    |   [stringlit_or_charlit]
    |   [charlit]
end define

define rerun_criterion
        [number] 'RECORDS [opt OF_file_name]
    |   [opt END_OF] [REEL_or_UNIT] [opt OF_file_name]
    |   [number] 'CLOCK-UNITS
    |   [id]
end define

define END_OF
	'END [opt 'OF]
end define

define REEL_or_UNIT
        'REEL | 'UNIT
end define

define OF_file_name
	[opt 'OF] [id]
end define

define same_clause
        'SAME [opt RECORD_or_SORT_or_SORT_MERGE] [opt 'AREA] [opt 'FOR] [id] [repeat id+]
end define

define RECORD_or_SORT_or_SORT_MERGE
        'RECORD | 'SORT  | 'SORT-MERGE
end define

define multiple_clause
        'MULTIPLE 'FILE [opt 'TAPE] [opt 'CONTAINS] [repeat file_name_position+]
end define

define file_name_position
        [id] [opt POSITION_integer]
end define

define POSITION_integer
        'POSITION [number]
end define

define apply_clause
        'APPLY [apply_property] [opt 'ON] [repeat id+]
end define

define apply_property
        'WRITE-ONLY 
    |   'CORE-INDEX
    |   'RECORD-OVERFLOW
    |   'REORG-CRITERIA [opt 'TO] [id]
end define


% =================================================
%            	Data Division 
% =================================================

define data_division
					[NL] [NL] [NL]
	'DATA 'DIVISION [dot]
        [opt file_section]		
        [opt working_storage_section]	
        [opt linkage_section]		
        [opt communication_section]		
        [opt report_section]		
end define


% =================================================
%      	Data Division - File Section 
% =================================================

define file_section
					[NL] [NL] 
	'FILE 'SECTION [dot] 						
	[repeat file_description_entry]
end define

define file_description_entry
	[file_description]
    |	[sort_merge_file_description]    
end define

define file_description
								[NL] [NL]
    'FD [SP] [decl_name] [opt IS_EXTERNAL] [opt IS_GLOBAL]	[IN] 
	[repeat file_description_clause] '.			[EX]
	[repeat record_description]
end define 

define IS_EXTERNAL
        [opt 'IS] 'EXTERNAL
end define

define IS_GLOBAL
        [opt 'IS] 'GLOBAL
end define

% RATH - added quasi-redundant IS_EXTERNAL to file_description_clause to 
% accommodate periodically occurring mis-ordering of the file description,
% which the compiler expects.  I left the [opt IS_EXTERNAL] in the file_description
% nonterminal to allow normal formatting.

define file_description_clause     
        [TAB_25] [block_clause]
    |	[TAB_25] [IS_EXTERNAL]
    |   [TAB_25] [record_clause]
    |   [TAB_25] [label_clause]
    |   [TAB_25] [data_clause]
    |   [TAB_25] [FD_value_clause]
    |   [TAB_25] [linage_clause]
    |   [TAB_25] [top_clause]
    |   [TAB_25] [bottom_clause]
    |   [TAB_25] [recording_clause]
    |   [TAB_25] [report_clause]
    |   [TAB_25] [code_set_clause]
end define

define block_clause
        'BLOCK [opt 'CONTAINS] [opt integer_TO] [number] [opt CHARACTERS_or_RECORDS]
end define

define integer_TO
        [number] 'TO
end define

define CHARACTERS_or_RECORDS
        'CHARACTERS | 'RECORDS
end define

define record_clause
        'RECORD [opt 'CONTAINS] [number] [opt TO_integer] [opt 'CHARACTERS]
   |    'RECORD [opt 'IS] 'VARYING [opt 'IN] [opt 'SIZE] 
   		[opt FROM_integer] [opt TO_integer] [opt 'CHARACTERS]
		[opt depending_clause]
end define

define FROM_integer
        [opt 'FROM] [number]
end define

define TO_integer
        'TO [number]
end define

define depending_clause
        'DEPENDING [opt 'ON] [id]
end define

define label_clause
       	'LABEL [RECORD_or_RECORDS] [opt IS_or_ARE] [label_type]
end define

define RECORD_or_RECORDS
        'RECORD | 'RECORDS
end define

define label_type
        'STANDARD
    |   'OMITTED
    |   [repeat id+]
end define

define FD_value_clause
        'VALUE 'OF [repeat id_IS_value+]
end define

define id_IS_value
        [NL] [id] [opt 'IS] [id_or_literal]
end define

define data_clause
        'DATA [RECORD_or_RECORDS] [opt IS_or_ARE] [repeat id+]
end define

define linage_clause
        'LINAGE [opt 'IS] [id_or_integer] [opt 'LINES] 
	    [opt footing_clause] [opt top_clause] [opt bottom_clause]
end define

define footing_clause
        [opt 'WITH] 'FOOTING [opt 'AT] [id_or_integer]
end define

define top_clause
        [opt 'LINES] [opt 'AT] 'TOP [id_or_integer]
end define

define bottom_clause
        [opt 'LINES] [opt 'AT] 'BOTTOM [id_or_integer]
end define

define recording_clause
        'RECORDING [opt 'MODE] [opt 'IS] [recording_mode]
end define

define recording_mode
        'F | 'V | 'U  | 'S
end define

define report_clause
        [REPORT_or_REPORTS] [opt IS_or_ARE] [repeat id+]
end define

define REPORT_or_REPORTS
	'REPORT | 'REPORTS
end define

define code_set_clause
        'CODE-SET [opt 'IS] [id]
end define

define sort_merge_file_description
							[NL] [NL]
    'SD [SP] [decl_name]									[IN] 
	[repeat sort_merge_description_clause] '.	[EX]
	[repeat record_description]
end define

define sort_merge_description_clause
        [TAB_25] [block_clause]
    |   [TAB_25] [record_clause]
    |   [TAB_25] [data_clause]
    |   [TAB_25] [label_clause]
    |   [TAB_25] [recording_clause]
    |   [TAB_25] [FD_value_clause]
    |   [TAB_25] [linage_clause]
    |   [TAB_25] [code_set_clause]
    |   [TAB_25] [sort_option_clause]
end define

define sort_option_clause
	'SORT-OPTION [opt 'IS] [id]
end define


% =================================================
%      Data Division - Working Storage Section
% =================================================

define  working_storage_section
						[NL] [NL]
	'WORKING-STORAGE 'SECTION [dot]
        [repeat record_description]
end define


% =================================================
%      Data Division - Linkage Section
% =================================================

define linkage_section
						[NL] [NL]
	'LINKAGE 'SECTION [dot]		
	[repeat record_description]	
end define


% =================================================
%      Data Division - Communication Section
% =================================================

define communication_section
							[NL] [NL]
	'COMMUNICATION 'SECTION [dot]		
	[repeat communication_description_entry]	
end define

define communication_description_entry
	[communication_description]
    
end define

define communication_description
       										[NL] [NL]
        'CD [SP] [id] [opt 'FOR] [opt 'INITIAL] [INPUT_or_OUTPUT_or_I_O]	[IN] 
	     [opt initial_properties] [dot]					[EX]
end define

define INPUT_or_OUTPUT_or_I_O
	'INPUT | 'OUTPUT | 'I_O
end define

define initial_properties
	[repeat initial_property+]
    |	[repeat id+]
end define

define initial_property
	[TAB_25] [opt 'SYMBOLIC] 'QUEUE [opt 'IS] [id]
    |	[TAB_25] [opt 'SYMBOLIC] 'SUB-QUEUE-1 [opt 'IS] [id]
    |	[TAB_25] [opt 'SYMBOLIC] 'SUB-QUEUE-2 [opt 'IS] [id]
    |	[TAB_25] [opt 'SYMBOLIC] 'SUB-QUEUE-3 [opt 'IS] [id]
    |	[TAB_25] [opt 'SYMBOLIC] 'SOURCE [opt 'IS] [id]
    |	[TAB_25] [opt 'SYMBOLIC] 'DESTINATION [opt 'IS] [id]
    |	[TAB_25] [opt 'SYMBOLIC] 'TERMINAL [opt 'IS] [id]
    |	[TAB_25] 'MESSAGE 'DATE [opt 'IS] [id]
    |	[TAB_25] 'MESSAGE 'TIME [opt 'IS] [id]
    |	[TAB_25] [opt 'MESSAGE] 'COUNT [opt 'IS] [id]
    |	[TAB_25] 'STATUS 'KEY [opt 'IS] [id]
    |	[TAB_25] 'ERROR 'KEY [opt 'IS] [id]
    |	[TAB_25] 'END 'KEY [opt 'IS] [id]
    |	[TAB_25] 'DESTINATION 'COUNT [opt 'IS] [id]
    |	[TAB_25] 'DESTINATION 'TABLE 'OCCURS [number] [opt 'TIMES] [opt INDEXED_BY_id]
    |	[TAB_25] 'TEXT 'LENGTH [opt 'IS] [id]
end define

%% T.D. why not indexed_clause???
define INDEXED_BY_id
	'INDEXED [opt 'BY] [repeat id+]
end define

% =================================================
%      Data Division - Report Section
% =================================================

define report_section
						[NL] [NL] 
	'REPORT 'SECTION [dot]		
	[repeat report_description_entry]	
end define

define report_description_entry
	[report_description] [repeat report_group_description]
    
end define

define report_description
       						[NL] [NL]
        'RD [SP] [id] [opt IS_GLOBAL] 		[IN]
		[opt code_clause]          
		[opt controls_clause]
		[opt page_clause] [dot]
        	[repeat record_description]	[EX]
end define

define code_clause
        [opt 'WITH] 'CODE [identifier_or_literal]
end define

define controls_clause
        [CONTROL_or_CONTROLS] [opt IS_or_ARE] [opt 'FINAL] [repeat id+]
end define

define CONTROL_or_CONTROLS
        'CONTROL | 'CONTROLS 
end define

define page_clause
        'PAGE [opt LIMIT_or_LIMITS] [opt IS_or_ARE] [number] [LINE_or_LINES] 
	    [opt page_heading] [opt first_detail_clause] [opt last_detail_clause] 
	    [opt page_footing_clause]
end define

define LIMIT_or_LIMITS
        'LIMITS | 'LIMIT 
end define

define LINE_or_LINES
        'LINE | 'LINES
end define

define page_heading
        'HEADING [number]
end define

define first_detail_clause
        'FIRST 'DETAIL [number] 
end define

define last_detail_clause
        'LAST 'DETAIL [number]
end define

define page_footing_clause
        'FOOTING [number]
end define

define report_group_description
	[level_1] [opt id]						[IN] 
	    [repeat report_group_description_clause] [dot]		[EX]
    |	
    	[level_number] [opt id] 					[IN]
	    [repeat report_group_description_clause] [dot]		[EX]
end define

define report_group_description_clause
	[NL] [line_number_clause]
    |	[NL] [next_group_clause]
    |	[NL] [type_clause]
    |	[NL] [report_usage_clause]
    |	[NL] [report_picture_clause]
    |	[NL] [report_sign_clause]
    |	[NL] [report_justification_clause]
    |	[NL] [blank_when_clause]
    |	[NL] [column_number_clause]
    |	[NL] [source_clause]
    |	[NL] [group_indicate_clause]
    |	[NL] [picture_clause]
    |	[NL] [value_clause]
end define

define line_number_clause
	'LINE [opt 'NUMBER] [opt 'IS] [line_number]
end define

define line_number
	[number] [opt ON_NEXT_PAGE]
    |	'PLUS [number]
    |	'NEXT 'PAGE
end define

define ON_NEXT_PAGE
	[opt 'ON] 'NEXT 'PAGE
end define

define next_group_clause
	'NEXT 'GROUP [opt 'IS] [next_group]
end define

define next_group
	[number]
    |	'PLUS [number]
    |	'NEXT 'PAGE
end define

define type_clause
	'TYPE [opt 'IS] [type]
end define

define type
	[REPORT_HEADING]
    |	[PAGE_HEADING]
    |	[CONTROL_HEADING] [id_or_FINAL]
    |	[DETAIL]
    |	[CONTROL_FOOTING] [id_or_FINAL]
    |	[PAGE_FOOTING]
    |	[REPORT_FOOTING]
end define

define REPORT_HEADING
	'REPORT 'HEADING  |  'RH
end define

define PAGE_HEADING
	'PAGE 'HEADING  |  'PH
end define

define CONTROL_HEADING
	'CONTROL 'HEADING  |  'CH
end define

define id_or_FINAL
	[id]  |  'FINAL
end define

define DETAIL
	'DETAIL |  'DE
end define

define CONTROL_FOOTING
	'CONTROL 'FOOTING  |  'CF
end define

define PAGE_FOOTING
	'PAGE 'FOOTING  |  'PF
end define

define REPORT_FOOTING
	'REPORT 'FOOTING  |  'RF
end define

define report_usage_clause
	'USAGE [opt 'IS] [DISPLAY_or_DISPLAY_1]
end define

define DISPLAY_or_DISPLAY_1
	'DISPLAY  |  'DISPLAY-1
end define

define report_picture_clause
	[PICTURE_or_PIC] [opt 'IS] [stringlit_or_charlit]
end define

define stringlit_or_charlit
	[stringlit]  |  [charlit]
end define

define report_sign_clause
	'SIGN [opt 'IS] [LEADING_or_TRAILING] 'SEPARATE [opt 'CHARACTER]
end define

define LEADING_or_TRAILING
	'LEADING  |  'TRAILING
end define

define report_justification_clause
	[JUSTIFIED_or_JUST] [opt 'RIGHT]
end define

define JUSTIFIED_or_JUST
	'JUSTIFIED  |  'JUST
end define

define blank_when_clause
    	'BLANK [opt 'WHEN] [ZERO_or_ZEROES]
end define

define column_number_clause
	'COLUMN [opt 'NUMBER] [opt 'IS] [number]
end define

define source_clause
	'SOURCE [opt 'IS] [identifier]
	[opt source_value]
	[repeat sum_clause]
	[opt reset_clause]
end define

define source_value
	'VALUE [opt 'IS] [literal]
end define

define sum_clause
	'SUM [repeat identifier+] [opt upon_clause]
end define

define upon_clause
	'UPON [repeat id+]
end define

define reset_clause
	'RESET [opt 'ON] [id_or_FINAL]
end define

define group_indicate_clause
	'GROUP [opt 'INDICATE]
end define


% =================================================
%     Data Division -- Data Description Entries
% =================================================
 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% New definition for records and items in cobol.  Single non-terminal replaces
%% the 5 non-terminals used in the previous grammmar.  It also fixes the parsing
%% of level 66 items.
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

define record_description        
      									[NL]
      [record_item]
      [repeat level_66_description] 
    | [exec_sql_clause]
end define

define record_item
	  								[NL]
    [level_number] [SP] [decl_name] 
      [repeat description_clause] '.					[IN]
	[repeat level_88_description] 
	[opt subfields]  						[EX]
end define

define subfields
    [SP] '[ [repeat record_item] [SP] ']
end define

% Explicit definition of the enumerated set of levels allows the parser
% to separate out level 01 entries from others. This allows easy
% identification of a single complete record description as a non-terminal.
% This non-terminal is used by rules that decode the record's heirarchical 
% structure from the level numbers.

%% level number need no longer be explicit with new bracketing.  level_1 is
%% preserved since it is still needed in report_group_description.  This
%% dependency will be removed in a future version.

define level_1
        '1 | '01
end define

define level_number
   [number]
end define

define level_66_description
	[NL] '66 [SP] [decl_name] [TAB_38] [renames_clause] '.
end define

define level_88_description
	[NL] '88 [SP] [decl_name] [TAB_38] [value_clause] '.  
end define

% =================================================
%     Data Division -- Picture Clauses
% =================================================
 
define picture_clause
        [TAB_32] [PICTURE_or_PIC] [opt 'IS] [SPOFF] [SP] [picture] [SPON]
end define

define PICTURE_or_PIC
	'PICTURE  |  'PIC
end define

define picture
    [stringlit]
end define


% =================================================
%    Data Division -- Record Description Clauses
% =================================================
 
define description_clause
        [picture_clause]
    |   [redefines_clause]
    |   [opt 'IS] 'EXTERNAL
    |   [opt 'IS] 'GLOBAL
    |   [usage_clause]
    |   [sign_clause]
    |	[synchronization_clause]
    |	[justification_clause]
    |	[blank_when_clause]
    |   [value_clause]
    |	[occurs_clause]
end define

define redefines_clause
    	[TAB_32] 'REDEFINES [id] [opt library]
end define

define library
    	[OF_or_IN] [id_or_literal]
end define

define OF_or_IN
    	'OF | 'IN
end define

define usage_clause
    	[opt USAGE_IS] [usage_type]
end define

define USAGE_IS
	'USAGE [opt 'IS]
end define

define usage_type
       [usage_comp]
  |    [usage_comp_1]          
  |    [usage_comp_2]          
  |    [usage_comp_3]          
  |    [usage_comp_4]
  |    'INDEX           
  |    'DISPLAY         
  |    'POINTER         
  |    'DISPLAY-1       
end define

define usage_comp
	'COMPUTATIONAL    |  'COMP    | 'BINARY          
end define

define usage_comp_1
	'COMPUTATIONAL-1  |  'COMP-1
end define

define usage_comp_2
	'COMPUTATIONAL-2  |  'COMP-2
end define

define usage_comp_3
	'COMPUTATIONAL-3  |  'COMP-3  |  'PACKED-DECIMAL
end define

define usage_comp_4
	'COMPUTATIONAL-4  |  'COMP-4          
end define

define occurs_clause					
	[TAB_32] 'OCCURS [number] [opt TO_number] [opt 'TIMES]
	    [repeat depending_ordering_indexed_clause] 	
end define

define depending_ordering_indexed_clause
	[TAB_32] [DEPENDING_ON_id] 
    |	[TAB_32] [ordering_clause] 
    |	[TAB_32] [indexed_clause]
end define

define TO_number
	'TO [number]
end define

define DEPENDING_ON_id
	'DEPENDING [opt 'ON] [ref_name]	% T.D.  Feb 6, 1997
end define

define ordering_clause
        [ASCENDING_or_DESCENDING] [opt 'KEY] [opt 'IS] [repeat id+]
end define

define ASCENDING_or_DESCENDING
        'ASCENDING | 'DESCENDING
end define

define indexed_clause
	'INDEXED [opt 'BY] [repeat decl_name+]
end define

define sign_clause
	[opt 'SIGN] [opt 'IS] [LEADING_or_TRAILING] [opt SEPARATE_CHARACTER]
end define

define SEPARATE_CHARACTER
	'SEPARATE [opt 'CHARACTER]
end define

define synchronization_clause
       [SYNCHRONIZED_or_SYNC] [opt LEFT_or_RIGHT]
end define

define SYNCHRONIZED_or_SYNC
        'SYNCHRONIZED | 'SYNC
end define

define LEFT_or_RIGHT
        'LEFT | 'RIGHT
end define

define justification_clause
    	[JUSTIFIED_or_JUST] [opt 'RIGHT]
end define

define ZERO_or_ZEROES
        'ZERO
  |     'ZEROS
  |     'ZEROES
end define

define value_clause
        [TAB_43] [VALUE_or_VALUES] [opt IS_or_ARE] 
	    [IN] [IN] [IN] [literal_operands] [EX] [EX] [EX]
end define

define VALUE_or_VALUES
	'VALUE  |  'VALUES
end define

define renames_clause
	'RENAMES [id] [opt THROUGH_id]
end define


% =================================================
%      		Procedure Division 
% =================================================

define procedure_division
				    				[NL] [NL] [NL]
	'PROCEDURE 'DIVISION [opt using_clause] [dot]					 			
        [opt declaratives]
	[opt section_body]
        [repeat section]
end define

define using_clause
	'USING [IN] [repeat procedure_parameter_item+] [EX]
end define

define procedure_parameter_item
	[opt BY_REFERENCE_or_VALUE] [id]
end define

define BY_REFERENCE_or_VALUE
	[opt 'BY] 'REFERENCE
    |	[opt 'BY] 'VALUE
end define

define declaratives
				    		[NL]
	'DECLARATIVES [dot]			[IN]						  
	    [repeat declarative_clause]		[EX] [NL]
	'END 'DECLARATIVES [dot]
end define

define declarative_clause
						[NL] [NL] 
	[id] 'SECTION [opt number] [dot]	[NL]					
	    'USE [use_clause] [dot] 
        [opt paragraph_body]
	[repeat paragraph]
end define

define use_clause
        [opt 'GLOBAL] 'AFTER [opt 'STANDARD] [EXCEPTION_or_ERROR]
        'PROCEDURE [opt 'ON] [exception_cause]
    |   
        [opt 'GLOBAL] [BEFORE_or_AFTER] [opt 'STANDARD] [opt BEGINNING_or_ENDING]
        [opt FILE_or_REEL_or_UNIT] 'LABEL 'PROCEDURE [opt 'ON]
        [exception_cause]
    |   
        [opt 'FOR] 'DEBUGGING [opt 'ON] [debugged_procedures]
    |
    	'BEFORE 'REPORTING [ref_name]
end define

define EXCEPTION_or_ERROR
        'EXCEPTION | 'ERROR
end define

define exception_cause
        [repeat id+]
    |   'INPUT
    |   'OUTPUT
    |   'I-O
    |   'EXTEND
end define

define BEGINNING_or_ENDING
        'BEGINNING | 'ENDING
end define

define FILE_or_REEL_or_UNIT
        'FILE | 'REEL | 'UNIT
end define

define debugged_procedures
        'ALL 'PROCEDURES | [repeat id+]
end define

define section
	[repeat section_header+]
	[section_body]
end define

define section_header
						[NL] [NL] 
	[id] 'SECTION [opt number] [dot]			 				
end define

define section_body
	[NL]
	[opt paragraph_body]
	[repeat paragraph]
end define

define paragraph
	[NL]
	[repeat paragraph_header+]
	[paragraph_body]
end define

define paragraph_header
	[NL] [id_or_integer] [dot] 
end define

define paragraph_body
	[IN] [repeat sentence] [EX]
end define

define sentence
        [repeat statement+] [dot]
end define


% =================================================
% 	Procedure Division -- Statements 
% =================================================

define statement
	[accept_statement]
    |	[add_statement]
    |	[alter_statement]
    |	[call_statement]
    |	[cancel_statement]
    |	[close_statement]
    |	[compute_statement]
    |	[continue_statement]
    |	[delete_statement]
    |	[disable_statement]
    |	[display_statement]
    |	[divide_statement]
    |	[enable_statement]
    |	[enter_statement]
    |	[entry_statement]
    |	[evaluate_statement]
    |	[examine_statement]
    |	[exhibit_statement]
    |	[exit_statement]
    |	[exit_program_statement]
    |	[generate_statement]
    |	[goback_statement]
    |	[goto_statement]
    |	[if_statement]			
    |	[initialize_statement]
    |	[initiate_statement]
    |	[inspect_statement]
    |	[merge_statement]
    |	[move_statement]
    |	[multiply_statement]
    |	[next_statement]
    |	[note_statement]
    |	[on_statement]
    |	[open_statement]
    |	[perform_statement]
    |	[purge_statement]
    |	[read_statement]
    |	[receive_statement]
    |	[release_statement]
    |	[return_statement]
    |	[rewrite_statement]
    |	[search_statement]		
    |	[send_statement]
    |	[service_statement]
    |	[set_statement]
    |	[sort_statement]
    |	[start_statement]
    |	[stop_statement]
    |	[string_statement]
    |	[subtract_statement]
    |	[suppress_statement]
    |	[terminate_statement]
    |	[transform_statement]
    |	[unstring_statement]
    |	[write_statement]
    |	[CICS_statement]
    |   [exec_statement]
end define


% EXEC CICS statement addition - JRC 7.5.96
% Revised to avoid parsing problems with [key] - JRC 22.5.96

define CICS_statement
						[NL]
	'EXEC 'CICS				[IN]
		[repeat CICS_commands]		[NL][EX]
	'END-EXEC
end define

define CICS_commands
	[NL] [CICS_command] [repeat CICS_options]
end define

define CICS_options
	[CICS_option] [opt CICS_argument]
end define

define CICS_option
	[id] | 'AFTER | 'ALTER | 'ALTERNATE | 'AT | 'CANCEL | 'CONTROL
    |	'COPY | 'DELETE | 'ENTER | 'ENTRY | 'EQUAL | 'FILE
    |	'FOR | 'FROM | 'INTO | 'LABEL | 'LAST | 'LIST
    |	'NEXT | 'NOTE | 'PAGE | 'PASSWORD | 'PROGRAM | 'READ
    |	'RECEIVE | 'RELEASE | 'REPLACE | 'RESET | 'SET | 'TERMINAL
    |	'TIME | 'TITLE | 'UNTIL | 'USING
end define

define CICS_argument
	'( [CICS_datavalue_or_name] ')
end define

define CICS_datavalue_or_name
	[opt ADDRESS_OF] [id] | [number] | [charlit] | [stringlit]
end define

define CICS_command
    % Direct from IBM CICS v4r1 manual - JRC 22.5.96

    % 1.12  CICS Commands by function
    % The following is a list of commands categorized according to the function
    % they perform.

    % 1.12.1  Abend support
	'ABEND
    |	'HANDLE 'ABEND

    % 1.12.2  APPC basic conversation
	'GDS 'ALLOCATE
    |	'GDS 'ASSIGN
    |	'GDS 'CONNECT 'PROCESS
    |	'GDS 'EXTRACT 'ATTRIBUTES
    |	'GDS 'EXTRACT 'PROCESS
    |	'GDS 'FREE
    |	'GDS 'ISSUE 'ABEND
    |	'GDS 'ISSUE 'CONFIRMATION
    |	'GDS 'ISSUE 'ERROR
    |	'GDS 'ISSUE 'PREPARE
    |	'GDS 'ISSUE 'SIGNAL
    |	'GDS 'RECEIVE
    |	'GDS 'SEND
    |	'GDS 'WAIT

    % 1.12.3  APPC mapped conversation
    |	'ALLOCATE
    |	'CONNECT 'PROCESS
    |	'CONVERSE
    |	'EXTRACT 'ATTRIBUTES
    |	'EXTRACT 'PROCESS
    |	'FREE
    |	'ISSUE 'ABEND
    |	'ISSUE 'CONFIRMATION
    |	'ISSUE 'ERROR
    |	'ISSUE 'PREPARE
    |	'ISSUE 'SIGNAL
    |	'RECEIVE
    |	'SEND
    |	'WAIT 'CONVID

    % 1.12.4  Authentication
    |	'CHANGE 'PASSWORD
    |	'SIGNOFF
    |	'SIGNON
    |	'VERIFY 'PASSWORD

    % 1.12.5  Batch data interchange
    |	'ISSUE 'ABORT
    |	'ISSUE 'ADD
    |	'ISSUE 'END
    |	'ISSUE 'ERASE
    |	'ISSUE 'NOTE
    |	'ISSUE 'QUERY
    |	'ISSUE 'RECEIVE
    |	'ISSUE 'REPLACE
    |	'ISSUE 'SEND
    |	'ISSUE 'WAIT

    % 1.12.6  BMS
    |	'PURGE 'MESSAGE
    |	'RECEIVE 'MAP
    |	'RECEIVE 'PARTN
    |	'ROUTE
    |	'SEND 'CONTROL
    |	'SEND 'MAP
    |	'SEND 'PAGE
    |	'SEND 'PARTNSET
    |	'SEND 'TEXT
    |	'SEND 'TEXT 'MAPPED
    |	'SEND 'TEXT 'NOEDIT

    % 1.12.7  Built-in functions
    |	'BIF 'DEEDIT

    % 1.12.8  Console support
    |	'WRITE 'OPERATOR

    % 1.12.9  Diagnostic services
    |	'DUMP 'TRANSACTION
    |	'ENTER 'TRACENUM

    % 1.12.10  Environment services
    |	'ADDRESS
    |	'ADDRESS 'SET
    |	'ASSIGN

    % 1.12.11  Exception support
    |	'HANDLE 'CONDITION
    |	'IGNORE 'CONDITION
    |	'POP 'HANDLE
    |	'PUSH 'HANDLE

    % 1.12.12  File control
    |	'DELETE
    |	'ENDBR
    |	'READ
    |	'READNEXT
    |	'READPREV
    |	'RESETBR
    |	'REWRITE
    |	'STARTBR
    |	'UNLOCK
    |	'WRITE

    % 1.12.13  Interval control
    |	'ASKTIME
    |	'CANCEL
    |	'DELAY
    |	'FORMATTIME
    |	'POST
    |	'RETRIEVE
    |	'START
    |	'WAIT EVENT

    % 1.12.14  Journaling
    |	'WAIT 'JOURNALNUM
    |	'WRITE 'JOURNALNUM

    % 1.12.15  Monitoring
    |	'MONITOR

    % 1.12.16  Program control
    |	'LINK
    |	'LOAD
    |	'RELEASE
    |	'RETURN
    |	'XCTL

    % 1.12.17  Security
    |	'QUERY 'SECURITY

    % 1.12.18  Spool Interface (JES)
    |	'SPOOLCLOSE
    |	'SPOOLOPEN 'INPUT
    |	'SPOOLOPEN 'OUTPUT
    |	'SPOOLREAD
    |	'SPOOLWRITE

    % 1.12.19  Storage control
    |	'FREEMAIN
    |	'GETMAIN

    % 1.12.20  Syncpoint
    |	'SYNCPOINT
    |	'SYNCPOINT 'ROLLBACK

    % 1.12.21  Task control
    |	'CHANGE 'TASK
    |	'DEQ
    |	'ENQ
    |	'SUSPEND
    |	'WAIT 'EXTERNAL
    |	'WAITCICS

    % 1.12.22  Temporary storage control
    |	'DELETEQ 'TS
    |	'READQ 'TS
    |	'WRITEQ 'TS

    % 1.12.23  Terminal control
    |	'ALLOCATE
    |	'BUILD 'ATTACH
    |	'CONVERSE
    |	'EXTRACT 'ATTACH
    |	'EXTRACT 'ATTRIBUTES
    |	'EXTRACT 'LOGONMSG
    |	'EXTRACT 'TCT
    |	'FREE
    |	'HANDLE 'AID
    |	'ISSUE 'COPY
    |	'ISSUE 'DISCONNECT
    |	'ISSUE 'ENDFILE
    |	'ISSUE 'ENDOUTPUT
    |	'ISSUE 'EODS
    |	'ISSUE 'ERASEAUP
    |	'ISSUE 'LOAD
    |	'ISSUE 'PASS
    |	'ISSUE 'PRINT
    |	'ISSUE 'RESET
    |	'ISSUE 'SIGNAL
    |	'POINT
    |	'RECEIVE
    |	'SEND
    |	'WAIT 'SIGNAL
    |	'WAIT 'TERMINAL

    % 1.12.24  Transient data
    |	'DELETEQ 'TD
    |	'READQ 'TD
    |	'WRITEQ 'TD
end define

define move_statement
    	[NL] 'MOVE [move_clause]
end define

define move_clause
        [operands] [repeat TO_operands+] 
    |   [CORRESPONDING_or_CORR] [identifier] 'TO [repeat identifier+]
end define

define TO_operands
    	'TO [operands]
end define

define CORRESPONDING_or_CORR
        'CORRESPONDING | 'CORR
end define

define if_statement
    	[NL] 'IF [condition_expression]
	    [then_clause]
	    [opt else_clause]
	[opt END_IF]
end define

define then_clause		
	[opt 'THEN] 			[IN_3] 
	    [repeat statement+]		[EX_3]
end define

define else_clause
					[NL] 
	[ELSE_or_OTHERWISE]		[IN_3] 
	    [repeat statement+]		[EX_3]
end define

define ELSE_or_OTHERWISE
        'ELSE | 'OTHERWISE
end define

define END_IF
	[NL] 'END-IF
end define

define perform_statement
    	[NL] 'PERFORM [perform_clause]
end define

define perform_clause
        [id] [opt THROUGH_id] [opt times_phrase]
    |   
        [opt times_phrase] 		[IN_3]
    	    [repeat statement]		[EX_3]
	[END_PERFORM]
end define

define END_PERFORM
	[NL] 'END-PERFORM
end define

define times_phrase
        [identifier_or_literal] 'TIMES
    |   [opt WITH_TEST] 'UNTIL [condition_expression]
    |   [opt WITH_TEST] 'VARYING [identifier] 'FROM [identifier_or_literal] 
            'BY [identifier_or_literal] 'UNTIL [condition_expression]
	    [repeat after_clause] 
end define

define WITH_TEST
    	[opt 'WITH] 'TEST [BEFORE_or_AFTER]
end define
 
define BEFORE_or_AFTER
    	'BEFORE | 'AFTER
end define

define after_clause
    	[NL] 'AFTER [identifier] 'FROM [identifier_or_literal]
	    'BY [identifier_or_literal] 'UNTIL [condition_expression]
end define

define accept_statement
    	[NL] 'ACCEPT [identifier] [opt accept_clause]
end define

define accept_clause
        'FROM [accept_source]
    |	[opt 'MESSAGE] 'COUNT
end define

define accept_source
        'DATE | 'DAY | 'DAY-OF-WEEK | 'TIME 
    |   [function_reference] | [identifier]
end define

define add_statement
    	[NL] 'ADD [add_clause]		[IN_3] 
	    [repeat size_error]		[EX_3]
	[opt END_ADD]
end define

define END_ADD
    	[NL] 'END-ADD
end define

define add_clause
        [operands] 'TO [operands] [opt GIVING_operands]
   |    [operands] [opt TO_operands] 'GIVING [operands]
   |    [CORRESPONDING_or_CORR] [identifier] 'TO [identifier] [opt 'ROUNDED]
end define

define GIVING_operands
        'GIVING [operands]
end define

define size_error
       [NL] [opt 'NOT] [opt 'ON] 'SIZE 'ERROR [IN_3] [repeat statement+] [EX_3]
end define

define alter_statement
    	[NL] 'ALTER [id] 'TO [opt PROCEED_TO] [id]
end define

define PROCEED_TO
        'PROCEED 'TO
end define

define call_statement
    	[NL] 'CALL [identifier_or_literal]	[IN_3] 
	    [opt call_using_clause] 
	    [opt on_exception] 			[EX_3]
	[opt END_CALL]
end define

define END_CALL
    	[NL] 'END-CALL
end define

define call_using_clause
	'USING [call_arguments] 
end define

define call_arguments
    	[repeat argument_clause+]
end define

define argument_clause
	[by_reference_clause] 
    |	[by_content_clause] 
end define

define by_reference_clause
        [opt BY_REFERENCE] [identifier]
    |	[opt BY_REFERENCE] 'ADDRESS 'OF [id]
end define

define BY_REFERENCE
	[opt 'BY] 'REFERENCE
end define

define by_content_clause
    	[opt 'BY] 'CONTENT [repeat content_item]
end define

define content_item
        [opt LENGTH_OF] [identifier]
    |   [literal]
end define

define LENGTH_OF
       'LENGTH 'OF
end define

define on_exception
        [NL] [opt 'NOT] [opt 'ON] 'EXCEPTION [IN_3] [repeat statement+] [EX_3]
    |   [NL] [opt 'ON] 'OVERFLOW [IN_3] [repeat statement+] [EX_3]
end define

define cancel_statement
    	[NL] 'CANCEL [repeat identifier_or_literal+]
end define

define close_statement
    	[NL] 'CLOSE [close_items]
end define

define close_items
	[repeat close_item+]
end define

define close_item
        [id] [opt close_disposition] 
end define

define close_disposition
        [REEL_or_UNIT] [opt disposition]
    |   [disposition]
end define

define disposition
	[opt 'WITH] 'LOCK
    |	[opt 'WITH] 'NO 'REWIND
    |	[opt 'WITH] 'DISP
    |	[opt 'FOR] 'REMOVAL
end define

define compute_statement
    	[NL] 'COMPUTE [operands] [equal] [arithmetic_expression]	[IN_3]
            	[repeat size_error] 					[EX_3]
	[opt END_COMPUTE]
end define

define END_COMPUTE
    	[NL] 'END-COMPUTE
end define

define operands                                                  
	[repeat operand+]
end define  

define operand
	[function_reference]
    |	[identifier_or_literal] [opt 'ROUNDED] 
end define
                                                                         
define literal_operands
	[repeat literal_THROUGH_literal+]
end define

define literal_THROUGH_literal
        [literal] [opt THROUGH_literal]
end define

define THROUGH_literal
        [THROUGH_or_THRU] [literal]
end define

define equal
        '= | 'EQUAL
end define

define continue_statement
    	[NL] 'CONTINUE
end define

define delete_statement
    	[NL] 'DELETE [id] [opt 'RECORD]		[IN_3] 
	    [repeat invalid_key] 	[EX_3]
	[opt END_DELETE]
end define

define END_DELETE
    	[NL] 'END-DELETE
end define

define invalid_key
        [NL] [opt 'NOT] 'INVALID [opt 'KEY] 	[IN_3] 
	    [repeat statement+] 		[EX_3] 
end define

define display_statement
    	[NL] 'DISPLAY [IN] [IN] [display_arguments] [EX] [opt UPON_id] [opt WITH_NO_ADVANCING] [EX]
end define

define display_arguments
	[repeat identifier_or_literal+]
end define

define UPON_id
	[NL] 'UPON [id]
end define

define WITH_NO_ADVANCING
	[NL] [opt 'WITH] 'NO 'ADVANCING
end define

define disable_statement
	'DISABLE [terminal] [id] [opt 'WITH] 'KEY [identifier_or_literal]
end define

define terminal
	'INPUT [opt 'TERMINAL]
    |	'I-O 'TERMINAL
    |	'OUTPUT
end define

define divide_statement
    	[NL] 'DIVIDE [divide_clause]	[IN_3]
	    [repeat size_error]		[EX_3]
	[opt END_DIVIDE] 
end define

define divide_clause
	[operand] [INTO_or_BY] [operands]
	    [opt GIVING_operands] 
	    [opt REMAINDER_operand] 
end define

define END_DIVIDE
    	[NL] 'END-DIVIDE
end define

define INTO_or_BY
    'INTO | 'BY
end define

define REMAINDER_operand
	'REMAINDER [operand]
end define

define enable_statement
	'ENABLE [terminal] [id] [opt 'WITH] 'KEY [identifier_or_literal]
end define

define enter_statement
    	[NL] 'ENTER [opt enter_target] 
end define

define enter_target
        'SQL | 'COBOL | 'CICS | 'LINKAGE | [id]
end define

define entry_statement
    	[NL] 'ENTRY [entry_name] [opt using_clause]
end define

define entry_name
	[id_or_literal]
end define

define evaluate_statement
    									[NL] 
	'EVALUATE [tag_expression] [repeat ALSO_tag_expression] 	[IN_3] 
	    [repeat when_clause]
	    [opt when_other] 						[EX_3]
	[opt END_EVALUATE]
end define

define END_EVALUATE
    	[NL] 'END-EVALUATE
end define

define tag_expression
        [identifier_or_literal]
    |   [arithmetic_expression]
    |   [condition_expression]
    |	[TRUE_or_FALSE]
end define

define ALSO_tag_expression
	'ALSO [tag_expression]
end define

define when_clause
        								[NL] 
	'WHEN [label_expression] [repeat ALSO_label_expression] 	[IN_3] 
	    [repeat statement]						[EX_3]
end define
 
define label_expression
        'ANY
    |   [condition_expression]
    |	'TRUE
    |	'FALSE
    |   [opt 'NOT] [label_value] [opt THROUGH_label_value]
end define

define label_value
        [identifier_or_literal] | [arithmetic_expression]
end define

define THROUGH_label_value
        [THROUGH_or_THRU] [label_value]
end define

define ALSO_label_expression
        [NL] 'ALSO [label_expression]
end define

define when_other
				[NL] 
	'WHEN 'OTHER 		[IN_3]
	    [repeat statement]	[EX_3]
end define

define examine_statement
    	[NL] 'EXAMINE [identifier] [tallying_or_replacing_clause]
end define

define tallying_or_replacing_clause
        'TALLYING [UNTIL_FIRST_or_ALL_or_LEADING_or_FIRST] [literal] [opt REPLACING_BY_literal]
    |   'REPLACING [UNTIL_FIRST_or_ALL_or_LEADING_or_FIRST] [literal] 'BY [literal]
end define

define UNTIL_FIRST_or_ALL_or_LEADING_or_FIRST
        'UNTIL 'FIRST | 'ALL | 'LEADING |'FIRST
end define

define REPLACING_BY_literal
        'REPLACING [opt 'BY] [literal]
end define

define exhibit_statement
    	[NL] 'EXHIBIT [opt NAMED_or_CHANGED] [repeat identifier_or_literal+]
end define

define NAMED_or_CHANGED
        'NAMED | 'CHANGED [opt 'NAMED]
end define

define exit_statement
    	[NL] 'EXIT
end define

define exit_program_statement
     	[NL] 'EXIT 'PROGRAM
end define

define generate_statement
	[NL] 'GENERATE [repeat id+] 
end define

define goback_statement
    	[NL] 'GOBACK
end define

define goto_statement
    	[NL] 'GO [opt 'TO] [id]
    |	[NL] 'GO [opt 'TO] [repeat id+]'DEPENDING [opt 'ON] [identifier]
end define

define initialize_statement
    	[NL] 'INITIALIZE [repeat identifier+] [opt REPLACING_replace_items]
end define

define REPLACING_replace_items
	'REPLACING [repeat replace_item+]
end define

define replace_item
	[replaced_type] [opt 'DATA] 'BY [identifier_or_literal]
end define

define replaced_type
        'ALPHABETIC
    |   'ALPHANUMERIC
    |   'NUMERIC
    |   'ALPHANUMERIC-EDITED 
    |   'NUMERIC-EDITED 
    |   'DBCS
    |   'EGCS
end define

define initiate_statement
	[NL] 'INITIATE [repeat id+]
end define

%% T.D. Feb 6, 1997
define inspect_statement
    	[NL] 'INSPECT [identifier] [repeat converting_or_replacing_or_tallying_clause]
end define

define converting_or_replacing_or_tallying_clause
    	'TALLYING  [repeat tallying_clause+] 
    |	'REPLACING [repeat replacing_clause+]
    |	'CONVERTING [identifier_or_literal] 'TO [identifier_or_literal] 
    	    [repeat position_criterion]
end define

define tallying_clause
        [identifier] 'FOR [repeat tally_criterion+] [opt ',]
end define

define tally_criterion
        'CHARACTERS [repeat position_criterion+]
    |   [ALL_or_LEADING] [repeat tallying_all_criterion+]
end define

define ALL_or_LEADING
        'ALL | 'LEADING
end define

define tallying_all_criterion
        [identifier_or_literal] [repeat position_criterion]
end define

define position_criterion
        [BEFORE_or_AFTER] [opt 'INITIAL] [identifier_or_literal]
end define

define replacing_clause
        'CHARACTERS 'BY [identifier_or_literal] [repeat position_criterion] [opt ',]
    |   [ALL_or_LEADING_or_FIRST] [repeat replacing_all_criterion+] [opt ',]
end define

define ALL_or_LEADING_or_FIRST
        'ALL | 'LEADING  | 'FIRST
end define

define replacing_all_criterion
        [identifier_or_literal] 'BY [identifier_or_literal] [repeat position_criterion]
end define

define merge_statement
    	[NL] 'MERGE [id] [repeat merge_order+] [opt collating_sequence] 
	    'USING [repeat id+] [merge_output]
end define

define merge_order
        [opt 'ON] [ASCENDING_or_DESCENDING] [opt 'KEY] [repeat id+]
end define

define collating_sequence
        [opt 'COLLATING] 'SEQUENCE [opt 'IS] [id]
end define

define merge_output
        'OUTPUT 'PROCEDURE [opt 'IS] [id] [opt THROUGH_id]
    |   'GIVING [repeat id+]
end define

define THROUGH_id
        [THROUGH_or_THRU] [id]
end define

define multiply_statement
    	[NL] 'MULTIPLY [multiply_clause] 	[IN_3]
	    [repeat size_error]			[EX_3]
	[opt END_MULTIPLY]
end define

define END_MULTIPLY
    	[NL] 'END-MULTIPLY
end define

define multiply_clause
        [operands] 'BY [operands] [opt GIVING_operands]
end define

define next_statement
	[NL] 'NEXT 'SENTENCE
end define

define note_statement
	[NL] 'NOTE [stringlit_or_charlit]
end define

define on_statement
    	[NL] 'ON [repeat identifier_or_literal+] 
	    [opt AND_EVERY_identifier_or_literal] 
	    [opt UNTIL_identifier_or_literal] 		[IN_3] 
		[repeat statement+]			[EX_3]
	    [opt on_else_clause]
end define

define on_else_clause					
	    						[NL]
	    [ELSE_or_OTHERWISE] 			[IN_3]
		[repeat statement+]			[EX_3]
end define

define AND_EVERY_identifier_or_literal
        'AND 'EVERY [identifier_or_literal]
end define

define UNTIL_identifier_or_literal
        'UNTIL [identifier_or_literal]
end define

define open_statement
    	[NL] 'OPEN [open_items]
end define

define open_items
	[repeat open_item+]
end define

define open_item
	[opt open_kind] [id] [opt open_disposition]
end define

define open_kind
	'INPUT | 'OUTPUT | 'I-O | 'EXTEND
end define

define open_disposition
	'REVERSED 
    |	[opt 'WITH] 'NO 'REWIND
    |	[opt 'WITH] 'LOCK
end define

define purge_statement
	[NL] 'PURGE [id]
end define

define read_statement
    	[NL] 'READ [id] [opt 'NEXT] [opt 'RECORD] 
	    [opt INTO_identifier]			[IN_3]
	    [repeat read_option]			[EX_3]
	[opt END_READ]
end define

define END_READ
    	[NL] 'END-READ
end define

define read_option
    	[invalid_key]
    |	[at_end]
    |	[key_is]
end define

define key_is
	[NL] [IN] 'KEY [opt 'IS] [id] [EX]
end define

define at_end
       	[NL] [opt 'NOT] [opt 'AT] 'END [IN_3] [repeat statement+] [EX_3]
end define

define INTO_identifier
	'INTO [identifier]
end define


define receive_statement
	[NL] 'RECEIVE [id] [MESSAGE_or_SEGMENT] 'INTO [identifier]	[IN_3]
	    [repeat no_data] 						[EX_3]
	[opt END_RECEIVE]
end define

define END_RECEIVE
    	[NL] 'END-RECEIVE
end define

define MESSAGE_or_SEGMENT
	'MESSAGE  |  'SEGMENT
end define

define no_data
	[opt 'WITH] [opt 'NO] 'DATA [repeat statement+]
end define

define release_statement
    	[NL] 'RELEASE [identifier] [opt FROM_identifier]
end define

define FROM_identifier
	'FROM [identifier]
end define

define return_statement
    	[NL] 'RETURN [id] [opt 'RECORD] [opt INTO_identifier]		[IN_3]
	    [repeat at_end]						[EX_3] 
	[opt END_RETURN]
end define

define END_RETURN
    	[NL] 'END-RETURN
end define

define rewrite_statement
    	[NL] 'REWRITE [identifier] [opt FROM_identifier]		[IN_3]
	    [repeat invalid_key] 					[EX_3]
	[opt END_REWRITE]
end define

define END_REWRITE
    	[NL] 'END-REWRITE
end define

define search_statement
    	[NL] 'SEARCH [opt 'ALL] [identifier] [opt varying_clause]	[IN_3] 
	    [repeat at_end] 
	    [repeat search_when_clause] 				[EX_3]
	[opt END_SEARCH]		
end define

define varying_clause
    	'VARYING [identifier]
end define

define search_when_clause
        						[NL] 
	'WHEN [condition_expression]	 		[IN_3] 
	    [repeat statement]				[EX_3]
end define

define END_SEARCH
    	[NL] 'END-SEARCH
end define

define send_statement
	[NL] 'SEND [id] 'FROM [identifier]
    |	[NL] 'SEND [id] [opt FROM_identifier] 'WITH [send_property]
       	    [opt advancing_clause] [opt REPLACING_LINE]
end define

define send_property
	[identifier]
    |	'ESI | 'EMI | 'EGI
end define

define advancing_clause
	[BEFORE_or_AFTER] [opt 'ADVANCING] [advancing_unit]
end define

define advancing_unit
	[identifier_or_integer] [opt LINE_or_LINES]
    |	'PAGE
end define

define identifier_or_integer
	[identifier] | [number]
end define

define REPLACING_LINE
	'REPLACING [opt 'LINE]
end define

define service_statement
	[NL] 'SERVICE 'LABEL
    |	[NL] 'SERVICE 'RELOAD [identifier]
end define

define set_statement
    	[NL] 'SET [repeat set_clause+]
end define

define set_clause
	[repeat set_operand+] [opt UP_or_DOWN] [TO_or_BY] [set_value]
end define

define set_operand
	[opt ADDRESS_OF] [identifier]
end define

define set_value
	[opt ADDRESS_OF] [identifier]
    |	[numeric_literal]
    |	'ON | 'OFF | 'TRUE | 'NULL | 'NULLS
end define

define ADDRESS_OF
	'ADDRESS 'OF
end define

define UP_or_DOWN
	'UP | 'DOWN
end define

define TO_or_BY
	'TO | 'BY
end define

define sort_statement
    	[NL] 'SORT [id] [repeat sort_order+] [opt with_duplicates]
            [opt collating_sequence] [sort_input] [sort_output]
end define

define sort_order
        [opt 'ON] [ASCENDING_or_DESCENDING] [opt 'KEY] [repeat identifier+]
end define

define with_duplicates
	[opt 'WITH] 'DUPLICATES [opt IN_ORDER]
end define

define IN_ORDER
	'IN 'ORDER
end define

define sort_input
	'USING [operands]
    |   'INPUT 'PROCEDURE [opt 'IS] [id] [opt THROUGH_id]
end define

define sort_output
	'GIVING [operands]
    |   'OUTPUT 'PROCEDURE [opt 'IS] [id] [opt THROUGH_id]
end define

define start_statement
    	[NL] 'START [id] [opt key_comparison]	[IN_3]
            [repeat invalid_key] 		[EX_3]
	[opt END_START]
end define

define END_START
    	[NL] 'END-START
end define

define key_comparison
	'KEY [opt 'IS] [opt 'NOT] [relational_operator] [identifier]
end define

define stop_statement
    	[NL] 'STOP 'RUN
    |	[NL] 'STOP [literal]
end define

define string_statement
    	[NL] 'STRING [repeat delimit_clause+] 'INTO [identifier]	[IN_3]
            [opt with_pointer] 
	    [repeat on_overflow] 					[EX_3]
	[opt END_STRING]
end define

define END_STRING
    	[NL] 'END-STRING
end define

define delimit_clause
	[repeat identifier_or_literal+] 'DELIMITED [opt 'BY] [identifier_or_literal_or_SIZE]
end define

define identifier_or_literal_or_SIZE
	'SIZE | [identifier_or_literal]
end define

define with_pointer
	[opt 'WITH] 'POINTER [identifier]
end define

define on_overflow
    	[NL] [opt 'NOT] [opt 'ON] 'OVERFLOW [IN_3] [repeat statement+] [EX_3]
end define

define subtract_statement
    	[NL] 'SUBTRACT [subtract_clause]	[IN_3] 
	    [repeat size_error]			[EX_3]
	[opt END_SUBTRACT]
end define

define END_SUBTRACT
    	[NL] 'END-SUBTRACT
end define

define subtract_clause
        [operands] 'FROM [operands] [opt GIVING_operands] 
   |    [CORRESPONDING_or_CORR] [identifier] 'FROM [identifier] [opt 'ROUNDED]
end define

define suppress_statement
	[NL] 'SUPPRESS [opt 'PRINTING]
end define

define terminate_statement
	[NL] 'TERMINATE [repeat id+]
end define

define transform_statement
    	[NL] 'TRANSFORM [identifier] [opt 'CHARACTERS]
	    'FROM [identifier_or_literal] 'TO [identifier_or_literal]
end define

define unstring_statement
    	[NL] 'UNSTRING [identifier] [opt unstring_delimit_clause]
	    'INTO [repeat identifier_opt_delimiter+]			[IN_3] 
	    [opt with_pointer]					 
	    [opt tallying] 
	    [repeat on_overflow] 					[EX_3]
	[opt END_UNSTRING]
end define

define END_UNSTRING
    	[NL] 'END-UNSTRING
end define

define unstring_delimit_clause
	'DELIMITED [opt 'BY] [opt 'ALL] [identifier_or_literal] 
	    [repeat OR_ALL_identifier_or_literal]
end define

define OR_ALL_identifier_or_literal
	'OR [opt 'ALL] [identifier_or_literal]
end define

define identifier_opt_delimiter
	[identifier] [opt DELIMITER_IN_identifier] [opt COUNT_IN_identifier]
end define

define DELIMITER_IN_identifier
	'DELIMITER [opt 'IN] [identifier]
end define

define COUNT_IN_identifier
	'COUNT [opt IN_or_IS] [identifier]
end define

define	IN_or_IS 
	'IN | 'IS
end define

define tallying
	'TALLYING [opt 'IN] [identifier]
end define

define write_statement
    	[NL] 'WRITE [identifier] [opt FROM_identifier]		[IN_3] 
	    [opt positioning_clause] 
	    [repeat at_end_of_page_or_invalid_key] 	[EX_3]
	[opt END_WRITE]
end define

define END_WRITE
    	[NL] 'END-WRITE
end define

define positioning_clause
	[BEFORE_or_AFTER] [opt ADVANCING_or_POSITIONING] [advancing_unit]
end define

define ADVANCING_or_POSITIONING
	'ADVANCING  |  'POSITIONING
end define

define at_end_of_page_or_invalid_key
	[at_end_of_page]
    |	[invalid_key]
end define

define at_end_of_page
	[NL] [opt 'NOT] 'AT [END_OF_PAGE_or_EOP] [IN_3] [repeat statement+] [EX_3] 
end define

define END_OF_PAGE_or_EOP
	'END-OF-PAGE  |  'EOP
end define

% ==========================================================
%			Expressions
% ==========================================================

define id_or_literal
        [id]
    |   [literal]
end define

define id_or_integer
        [id] 
    |	[number]
end define

define identifier_or_literal
        [identifier]
    |   [literal]
end define

define literal
     	[stringlit_or_charlit]
    |   [fig_const]
    |   [numeric_literal]
    |	[hex_literal]
end define 

define numeric_literal
         [number]
%     |   [sign] [SPOFF] [number] [SPON]
end define

define hex_literal
	 'X [SPOFF] [charlit] [SPON]
end define

define fig_const
        'SPACE | 'SPACES
    |   [ZERO_or_ZEROES]
    |   'HIGH-VALUE | 'HIGH-VALUES 
    |   'LOW-VALUE | 'LOW-VALUES 
    |   'QUOTE  | 'QUOTES
    |   'ALL [fig_const]
    |	[stringlit_or_charlit]
    |   'NULL  | 'NULLS
    |   'TRUE  | 'FALSE
    | 	'END |  'EOP  |  'END-OF-PAGE | 'INVALID [opt 'KEY] | 'DATA
    | 	'SIZE 'ERROR  |  'OVERFLOW    |   'EXCEPTION
end define


define function_reference
        'FUNCTION [function_name] [opt func_arg_seq] [opt position_selector]
end define

define func_arg_seq
        '( [repeat function_argument] ')
end define

define function_argument
        [identifier_or_literal]
    |   [arithmetic_expression]
end define

define function_name
        [id]
end define

define condition_expression
       [condition_term] [repeat or_condition_term]
end define

define or_condition_term
	[IN] [IN] [NL] 'OR [condition_term] [EX] [EX]
end define

define condition_term
        [condition_factor] [repeat and_condition_factor]
end define

define and_condition_factor
	[IN] [IN] [NL] 'AND [condition_factor] [EX] [EX]
end define

define condition_factor
	[opt 'NOT] [condition_primary]
end define

define condition_primary
	( [condition_expression] )
    |	[TRUE_or_FALSE]
    |	[class_condition]
    |	[sign_condition]
    |	[relation_condition]
    |	[io_condition]
    |	[error_condition]
    |	[level_88_condition]
end define

define level_88_condition
	'? [identifier] %[opt IS_TRUE_or_FALSE]
end define

define TRUE_or_FALSE
	'TRUE  |  'FALSE
end define

define io_condition
	'EOF [opt IS_TRUE_or_FALSE] | 'END [opt IS_TRUE_or_FALSE] | 'INVALID [opt 'KEY]
end define

define error_condition
	'SIZE 'ERROR
end define

define IS_TRUE_or_FALSE
	'IS 'FALSE
    |	'IS 'TRUE
end define

% Change by Russ Halliday, August 9, 1996
%
%define class_condition
%	[identifier] [opt 'IS] [opt 'NOT] [class_type]
%end define
%
% becomes:

define class_condition
	[identifier_or_bracketed_identifier] [opt 'IS] [opt 'NOT] [class_type]
end define

define identifier_or_bracketed_identifier
	[identifier]
     |  '( [identifier] ')
end define

% Looks great, eh?  :(


define class_type
        'NUMERIC 
    |   'ALPHABETIC
    |   'ALPHABETIC-LOWER
    |   'ALPHABETIC-UPPER
    |   [id]
    |   'DBCS 
    |   'KANJI
end define

define relation_condition
	[subject] [opt 'IS] [predicate]	
end define

define subject
	[arithmetic_expression]
    |	'ADDRESS 'OF [identifier]
end define

define predicate
	[predicate_term] [repeat OR_predicate_term]
end define

define OR_predicate_term
	'OR [opt 'IS] [predicate_term]
end define

define predicate_term
	[predicate_factor] [repeat AND_predicate_factor]
end define

define AND_predicate_factor
	'AND [opt 'IS] [predicate_factor]
end define

define predicate_factor
	[opt 'NOT] [predicate_primary]
end define

define predicate_primary
	[relational_operator] [objects]
    |	( [predicate] )
    |   [fig_const]
end define

define objects
	[object_term] [repeat OR_object_term]
end define

define OR_object_term
	'OR [object_term]
end define

define object_term
	[object_factor] [repeat AND_object_factor]
end define

define AND_object_factor
	'AND [object_factor]
end define

define object_factor
	[opt 'NOT] [opt 'IS] [object_primary]
end define

define object_primary
	( [objects] )
    |	[subject] 
end define

define relational_operator
	[equality]
    |	[inequality]
end define

define inequality
	[inequality_sign] [opt 'THAN] [opt OR_equality]
    |	'>=
    |	'<=
end define

define inequality_sign
	'GREATER | '> | 'LESS | '<
end define

define equality
	[equality_sign] [opt 'TO]
end define

define equality_sign
	'= | 'EQUAL | 'EQUALS
end define

define OR_equality
	'OR [equality]
end define

define sign_condition                                    
    	[arithmetic_expression] [opt 'IS] [opt 'NOT] [POSITIVE_or_NEGATIVE_or_ZERO]           
end define                                               
                                                         
define POSITIVE_or_NEGATIVE_or_ZERO                                      
    	'POSITIVE | 'NEGATIVE | 'ZERO                        
end define                                               
                                                         
define arithmetic_expression
        [arithmetic_term] [repeat add_subtract_op_term] 
end define

define add_subtract_op_term
	[add_or_subtract] [arithmetic_term]
end define

define add_or_subtract
    	[SPOFF] [SP] '+ [SP] [SPON] | [SPOFF] [SP] '- [SP] [SPON]
end define

define arithmetic_term
        [arithmetic_factor] [repeat mult_div_factor] 
end define

define mult_div_factor
	[multiply_or_divide] [arithmetic_factor]
end define

define multiply_or_divide
    	'* | '/
end define

define arithmetic_factor
        [arithmetic_primary] [repeat exponentiation]
end define

define exponentiation
	[exponentiate] [arithmetic_primary]
end define

define exponentiate
    	[SPOFF] [SP] '** [SPON]
end define

define arithmetic_primary
	[literal]
    |	[opt sign] [identifier]
    |   [opt sign] '( [arithmetic_expression] ')
    |   [function_reference]
end define

define sign
	'+ | '-
end define

define dot
	'.
end define

% ==========================================================
%			Names And References
% ==========================================================

%% Two new non-terminals are used to parse names: decl_name
%% and ref_name.  Decl_name is used in contexts where a name
%% is introduced such as file and field definitionsl.  Reference
%% name is uses where a qualified name may be used to refer to
%% another field such as a operand in a statement, or a subscript.
%% it is not the same as an identifier, since a reference name
%% may not be subscripted or position selected.  The definition of
%% identifier will use the non-terminal ref_name.

%% Note.  Currently, there are several parts in the grammar and in the
%% overrides, where identifier is used incorrectly. ref_name should
%% be used instead.

define decl_name
    [name]
end define

define ref_name
	[name]
end define

define name
      [id] [repeat qualifier]
end define

define qualifier
       [OF_or_IN_id] 
end define

define OF_or_IN_id
        'OF [id]
    |   'IN [id]
end define

%% Identifier Grammar - full references in for the procedure division.

define identifier
    [ref_name] [opt subscript] [opt position_selector]
end define

define subscript
	   '( [repeat subscript_expression] ')
end define

define subscript_expression
        [numeric_literal]
    |   [ref_name] [opt numeric_literal_offset]
end define

define numeric_literal_offset
    [sign] [numeric_literal]
end define

define position_selector
	'( [left_char_position] ': [opt length] ')
end define

define left_char_position
    	[arithmetic_expression]
end define

define length
    	[arithmetic_expression]
end define

% ==========================================================
%			General Exec Grammar
% ==========================================================

define exec_sql_clause
        [NL] 'EXEC		 		[IN]
            [exec_items] '.			[NL]
end define

define exec_statement
        [NL] 'EXEC 				[IN]
            [exec_items] 
end define

define exec_items
      [EX][NL] END-EXEC [KEEP]
    | EXEC [KEEP]  % force a syntax error if we find another exec.
                   % most likely caused by an EXEC SQL terminated by
		   % a period instead of an END-EXEC.
    | [token] [exec_items]
    | [NL] [key] [exec_items]
end define

% ===== End of grammar =====



